# The data build mostly on AMB-profil. We only need some adjustments.

# -------  image ----
#There are no pictures there at the moment
unless exists("image")
  copy_field("id", "image")
  prepend("image", "https://api.edoweb-test.hbz-nrw.de/tools/thumby?url=")
  append("image","/data&size=250")
end


# -------  id ----
# Needs ajustment to link to the landingPage and not to fedora
replace_all("id", ".*orca\\:", "https://www.orca.nrw/content/")


# ------- creator/contributor-affiliation ------

do list(path: "creator[]", "var": "$i")
	unless any_contain("$i.id","orcid")
		remove_field("$i.id")
	end
	copy_field("$i.affiliation.id", "$i.affiliation.name")
end

lookup("creator[].*.affiliation.name", "data/maps/orcaUni.tsv", "sep_char":"\t", delete:"true")

do list(path: "contributor[]", "var": "$i")
	unless any_contain("$i.id","orcid")
		remove_field("$i.id")
	end
	copy_field("$i.affiliation.id", "$i.affiliation.name")
end


lookup("contributor[].*.affiliation.name", "data/maps/orcaUni.tsv", "sep_char":"\t", delete:"true")


# ------------ sourceOrganization -------------
set_array("@sourceOrga")
copy_field("contributor[].*.affiliation.id", "@sourceOrga.$append")
copy_field("creator[].*.affiliation.id", "@sourceOrga.$append")
uniq("@sourceOrga") 

set_array("sourceOrganization[]")
do list(path: "@sourceOrga", "var": "$i")
	copy_field("$i", "sourceOrganization[].$append.id")
	copy_field("$i", "sourceOrganization[].$last.name")
	add_field("sourceOrganization[].$last.type", "Organization")
end
	
lookup("sourceOrganization[].*.name", "data/maps/orcaUni.tsv", "sep_char":"\t", delete:"true")


do list(path: "learningResourceType[]")
	lookup("id","data/maps/orcaMedienId-hcrt.tsv", "sep_char":"\t", delete:"true")
	remove_field("prefLabel") # do not know if that is necessary, when rewriten
	copy_field("id", "prefLabel.de")
	lookup("prefLabel.de","data/maps/hcrt-de-labels.tsv", "sep_char":"\t", delete:"true")
	copy_field("id", "prefLabel.en")
	lookup("prefLabel.en","data/maps/hcrt-en-labels.tsv", "sep_char":"\t", delete:"true")
end

replace_all("learningResourceType[].*.inScheme.id", "https://w3id.org/orca.nrw/medientypen/", "https://w3id.org/kim/hcrt/scheme")

# ---------mainEntityOfPage----------------
set_array("mainEntityOfPage[]")
copy_field("id", "mainEntityOfPage[].$append.id")
add_field("mainEntityOfPage[].$last.provider.id","$[service_id]")
add_field("mainEntityOfPage[].$last.provider.type","Service")
add_field("mainEntityOfPage[].$last.provider.name","$[service_name]")

# ------------ inLanguage -------------
# needs to be patched
remove_field("inLanguage[]")
set_array("inLanguage[]", "de")


# ---------------@context ----------------------------------

set_array("@context[]", "https://w3id.org/kim/amb/draft/context.jsonld")
# resource is german therefore prefer de if multiple or no inLanguage are available
if any_equal("inLanguage[]","de")
  set_hash("@context[].$append", "@language": "de")
elsif any_equal("inLanguage[]","en")
  set_hash("@context[].$append", "@language": "en")
else
  copy_field("inLanguage[].1", "@context[].$append.@language")
end
# resources without inLanguage-Values set default to de
unless exists("inLanguage[].1")
  set_hash("@context[].$append", "@language": "de")
end

# --- retain needs the exact name as it enters, so creator[]
retain("@context[]","id","image","name", "license", "creator[]","contributor[]","sourceOrganization[]","description","inLanguage[]","type[]","learningResourceType[]","keywords[]","about[]", "encoding[]", "mainEntityOfPage[]" )
vacuum()
