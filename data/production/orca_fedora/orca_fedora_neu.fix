# The data build mostly on AMB-profil. We only need some adjustments.

# ---------------@context ----------------------------------

set_array("@context[]", "https://w3id.org/kim/lrmi-profile/draft/context.jsonld")
set_hash("@context[].$append", "@language": "de")


# -------  image ----
#There are no pictures there at the moment
unless exists("image")
  copy_field("id", "image")
  prepend("image", "https://api.edoweb-test.hbz-nrw.de/tools/thumby?url=")
  append("image","/data&size=250")
end


# -------  id ----
# Needs ajustment to link to the landingPage and not to fedora
replace_all("id", ".*orca\\:", "https://www.orca.nrw/content/")


# ------------ sourceOrganization -------------
set_array("@sourceOrga")
copy_field("contributor[].*.affiliation.id", "@sourceOrga.$append")
copy_field("creator[].*.affiliation.id", "@sourceOrga.$append")
uniq("@sourceOrga") 

set_array("sourceOrganization[]")
do list(path: "@sourceOrga", "var": "$i")
	copy_field("$i", "sourceOrganization[].$append.id")
	copy_field("$i", "sourceOrganization[].$last.name")
	lookup("sourceOrganization[].$last.name", "data/maps/orcaUni.tsv", "sep_char":"\t")
	add_field("sourceOrganization[].$last.type", "Organization")
end


do list(path: "learningResourceType[]")
	lookup("id","data/maps/orcaMedienId-hcrt.tsv", "sep_char":"\t")
	remove_field("prefLabel") # do not know if that is necessary, when rewriten
	copy_field("id", "prefLabel.de")
	lookup("prefLabel.de","data/maps/hcrt-de-labels.tsv", "sep_char":"\t")
	copy_field("id", "prefLabel.en")
	lookup("prefLabel.en","data/maps/hcrt-en-labels.tsv", "sep_char":"\t")
end

replace_all("learningResourceType[].*.inScheme.id", "https://w3id.org/orca.nrw/medientypen/", "https://w3id.org/kim/hcrt/scheme")


/*encoding - NEEDS TO BE UPDATED IF ORCA STARTS TO BE A REFERALTORY TOO. */

set_array("encoding[]")
copy_field("id", "encoding[].$append.contentUrl")
append("encoding[].*.id", "/data")
add_field("encoding[].$last.type", "MediaObject")



# ---------mainEntityOfPage----------------
set_array("mainEntityOfPage[]")
copy_field("id", "mainEntityOfPage[].$append.id")
add_field("mainEntityOfPage[].$last.provider.id","$[service_id]")
add_field("mainEntityOfPage[].$last.provider.type","Service")
add_field("mainEntityOfPage[].$last.provider.name","$[service_name]")

# --- retain needs the exact name as it enters, so creator[]
retain("@context[]","id","image","name", "license", "creator[]","contributor[]","sourceOrganization[]","description","inLanguage[]","type[]","learningResourceType[]","keywords[]","about[]", "encoding[]", "mainEntityOfPage[]" )
vacuum()