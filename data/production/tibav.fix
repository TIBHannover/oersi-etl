/* The difficulty with the fix is that the attributes are read as values but not as keys.*/
/* Also that values and the attribute are not grouped together as objects but all are single keys at the same level. */

/* Set up the context */
do array("@context")
 add_field("","https://w3id.org/kim/lrmi-profile/draft/context.jsonld")
 do entity("")
  add_field("@language", "de")
 end
end

/* id: Construct actual ID from @id. Picked from header not from record itself. */
do map("html.body.record.header.identifier.value","id")
    replace_all(pattern: "oai:tib.eu:kmo-av:sid~", with: "https://av.tib.eu/media/")
end

/* name:  */
do map("html.body.record.metadata.resources.resource.titles.title.value", "name")
  not_equals(string: "")
end

/*image*/
do map("html.body.record.header.identifier.value","image")
    replace_all(pattern: "oai:tib.eu:kmo-av:sid~", with: "https://av.tib.eu/thumbnail/")
end

/* dateCreated: */
map("html.body.record.metadata.resources.resource.dates.date.Issued", "dateCreated")

/*description:*/
do map("html.body.record.metadata.resources.resource.descriptions.description.Abstract", "description")
  replace_all(pattern: "^\\(de\\)|^\\(en\\)", with: "")
end

do map("html.body.record.metadata.resources.resource.rightslist.rights.rightsuri","license")
  regexp(match: "https://creativecommons.org/(licenses|licences|publicdomain)/.*")
end

do array ("type")
  add_field("","LearningResource")
end

/* Add Video always as lRT */

add_field("@hcrt","https://w3id.org/kim/hcrt/video")

do array('learningResourceType')
  do entity('')
    map('@hcrt', 'id')
    do entity('prefLabel')
      do map('@hcrt', 'de')
        lookup(in: 'data/maps/hcrt-de-labels.tsv')
      end  
      do map('@hcrt', 'en')
        lookup(in: 'data/maps/hcrt-en-labels.tsv')
      end
    end
  end
end

/*publisher:  May exist more than once. */
do array("publisher")
 do entity("")
  add_field("type", "Organization")
  map("html.body.record.metadata.resources.resource.publisher.value", "name")
 end
end
/* mainEntityofPage */

do array("mainEntityOfPage")
  do entity("")
    do map("html.body.record.header.identifier.value","id")
        replace_all(pattern: "oai:tib.eu:kmo-av:sid~", with: "https://av.tib.eu/media/")
    end
    /* Add provider/source information to each resource description */
    do entity("provider")
      add_field("id","$[service_id]")
      add_field("type","Service")
      add_field("name","$[service_name]")
    end
  end
end

/* inLanguage */
do array("inLanguage")
  do map("html.body.record.metadata.resources.resource.language.value")
    replace_all(pattern: "http://id.loc.gov/vocabulary/iso639-1/", with: "")
  end
end

/* TIB does not differentiate between an organisation and a person. */
do array("creator", flushWith: record)
 do entity("")
 /* If TIB would provide type, we could try. At the moment FIX does not support conditionals so that we only can recognise creators as Person */
  add_field("type", "Person")
  do map("html.body.record.metadata.resources.resource.creators.creator.creatorname.value", "name")
    replace(pattern: "(.+), (.+)", with: "$2 $1")
  end
 end
end

/*All subjects without attributes */
do entity('keywords[]', flushWith: 'record')
 map('html.body.record.metadata.resources.resource.subjects.subject.value')
end

/*All subjects with knm or linsearch attribute are transformed to about objects. Duplicate objects should be avoided. kmn and linsearch are almoist identical. */
do map('html.body.record.metadata.resources.resource.subjects.subject.linsearch|html.body.record.metadata.resources.resource.subjects.subject.knm','@hochschulfaechersystematik')
  lookup(in: 'data/maps/tibav-subject-mapping.tsv')
  unique()
end
  
do array('about', flushWith: record)
  do entity('', flushWith: '@hochschulfaechersystematik')
    map('@hochschulfaechersystematik', 'id')
    do map('@hochschulfaechersystematik', 'prefLabel.de')
      lookup(in: 'data/maps/subject-labels.tsv')
    end
  end
end