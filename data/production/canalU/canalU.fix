# Reject deleted, broken, unsufficient records as well as iMoox resources

if exists ("metadata.error")
  reject()
end

if any_equal ("header.status", "deleted")
  reject()
end

# Delete all empty values
vacuum()

do once("macrosAndMaps")
  include ("../../sharedFixes/macros.fix")
  include ("../../sharedFixes/maps.fix")
  # workflow specific map
  put_filemap("data/maps/canalUDiscipline2Destatis.tsv","canalUDiscipline2Destatis","sep_char":"\t",key_column:"0",value_column:"2",expected_columns:"-1")
end

# --------------id ----------------------------------------

do list(path: "metadata.oai_dc:dc.dc:source", "var": "$i")
  if any_contain("$i.value","doi.org")
    copy_field("$i.value","@doi")
  end
end




do list(path: "metadata.oai_dc:dc.dc:identifier", "var": "$i")
  if any_contain("$i.value","https://www.canal-u.tv")
    copy_field("$i.value","@internalId")
  end
end


if exists("@doi")
  copy_field("@doi","id")
else
  copy_field("@internalId","id")
end

#  ------------image ------------------------------------
unless exists("image")
  add_field("image","https://www.canal-u.tv/themes/custom/canalu/logo.svg")
end

#  ------------name ------------------------------------
copy_field("metadata.oai_dc:dc.dc:title.value","name")



#  ------------creator------------------------------------
add_array("creator[]")
do list(path:"metadata.oai_dc:dc.dc:creator","var":"$i")
  copy_field("$i.value","creator[].$append.name")
  add_field("creator[].$last.type","Person")
end

replace_all("creator[].*.name","(.+), (.+)", "$2 $1")

# No specifics about creator type. Set type=Person as default.
# add_field("creator[].*.type", "Person")
call_macro("deleteAcademicTitles", field: "creator[].*.name")

#  ------------contributor------------------------------------
add_array("contributor[]")
do list(path:"metadata.oai_dc:dc.dc:contributor","var":"$i")
  copy_field("$i.value","contributor[].$append.name")
  add_field("contributor[].$last.type","Person")
end

replace_all("contributor[].*.name","(.+), (.+)", "$2 $1")

# ----- Publisher -----

add_array("publisher[]")
copy_field("metadata.oai_dc:dc.dc:publisher.value","publisher[].$append.name")
add_field("publisher[].*.type", "Organization")


add_array("sourceOrganization[]")
add_field("sourceOrganization[].$append.name", "Technische Universit√§t Graz")
add_field("sourceOrganization[].$last.type", "Organization")
add_field("sourceOrganization[].$last.id", "https://ror.org/00d7xrm67")

#  ------------description------------------------------------
copy_field("metadata.oai_dc:dc.dc:description.value","description")

#------------keywords---------

add_array("keywords[]")
do list("path": "metadata.oai_dc:dc.dc:subject", "var": "$i")
  copy_field("$i.value","keywords[].$append")
end

# -----------license----------
do list(path:"metadata.oai_dc:dc.dc:license","var":"$i")
  unless exists("license")
    copy_field("$i.value","license.id")
  end
end

#  ------------dataCreated------------------------------------
copy_field("metadata.oai_dc:dc.dc:created.value","dateCreated")
replace_all("dateCreated","^(\\d{4})$","$1-01-01")

#  ------------datePublished------------------------------------
copy_field("metadata.oai_dc:dc.dc:issued.value","datePublished")
replace_all("datePublished","^(\\d{4})$","$1-01-01")

#  ------------dateModified------------------------------------
copy_field("metadata.oai_dc:dc.dc:modified.value","dateModified")
replace_all("dateModified","^(\\d{4})$","$1-01-01")

#  ------------inLanguage------------------------------------
add_array("inLanguage[]")
do list("path": "metadata.oai_dc:dc.dc:language", "var": "$i")
  copy_field("$i.value", "inLanguage[].$append")
end

# ---------------type ----------------------------------
add_array("type[]", "LearningResource")

# ---------------learningResourceType ----------------------------------
add_array("learningResourceType[]")

lookup("metadata.oai_dc:dc.dc:type.value", 
"MovingImage":"https://w3id.org/kim/hcrt/video",
"Sound":"https://w3id.org/kim/hcrt/audio",
"Event":"https://w3id.org/kim/hcrt/other",
"InteractiveResource":"https://w3id.org/kim/hcrt/other")

do list(path: "metadata.oai_dc:dc.dc:type", "var":"$i")
  copy_field("$i.value","learningResourceType[].$append.id")
  copy_field("$i.value","learningResourceType[].$last.prefLabel.de")
  copy_field("$i.value","learningResourceType[].$last.prefLabel.en")
end

lookup("learningResourceType[].*.prefLabel.de", "hcrt-deLabel2Uri", delete:"true")
lookup("learningResourceType[].*.prefLabel.en", "hcrt-enLabel2Uri", delete:"true")


#  ------------about------------------------------------
add_array("about[]")
do list(path:"header.setSpec","var":"$i")
    do list(path: "$i.value", "var":"$j")
      if any_contain("$j","discipline")
        copy_field("$j","about[].$append.id")
        lookup("about[].$last.id", "canalUDiscipline2Destatis")
      end
    end
end

do list(path: "about[]", "var":"$i")
  copy_field("$i.id", "$i.prefLabel.de")
  lookup("$i.prefLabel.de", "destatis-deLabel2Uri", delete:"true")
end

# ----duration---
copy_field("metadata.oai_dc:dc.dc:extent.value","duration")


# -------isPartOf/hasPart---------
add_array("isPartOf[]")
add_array("hasPart[]")

do list("path": "metadata.oai_dc:dc.dc:isPartOf", "var": "$i")
  copy_field("$i.value", "isPartOf[].$append.id")
end

do list("path": "metadata.oai_dc:dc.dc:hasPart", "var": "$i")
  copy_field("$i.value", "hasPart[].$append.id")
end


# ---------------conditionsOfAccess----------------------------------
add_field("conditionsOfAccess.id","http://w3id.org/kim/conditionsOfAccess/no_login") 


# ---------mainEntityOfPage----------------
add_array("mainEntityOfPage[]")
copy_field("@internalId", "mainEntityOfPage[].$append.id")
add_field("mainEntityOfPage[].$last.provider.id","$[service_id]")
add_field("mainEntityOfPage[].$last.provider.type","Service")
add_field("mainEntityOfPage[].$last.provider.name","$[service_name]")

# ---------------@context ----------------------------------

add_array("@context[]", "https://w3id.org/kim/amb/context.jsonld")
# resource is german therefore prefer de if multiple or no inLanguage are available
if any_equal("inLanguage[]","de")
  add_hash("@context[].$append", "@language": "de")
elsif any_equal("inLanguage[]","en")
  add_hash("@context[].$append", "@language": "en")
else
  copy_field("inLanguage[].1", "@context[].$append.@language")
end
# resources without inLanguage-Values set default to de
unless exists("inLanguage[].1")
  add_hash("@context[].$append", "@language": "de")
end

# ---------tidy up!----------------
include ("../../sharedFixes/cleanUp.fix")
