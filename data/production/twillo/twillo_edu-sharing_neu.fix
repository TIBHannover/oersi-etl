# ---------------@context ----------------------------------

set_array("@context[]", "https://w3id.org/kim/lrmi-profile/draft/context.jsonld")
set_hash("@context[].$append", "@language": "de")

# ---------------name ----------------------------------
copy_field("node.title", "name")

# ---------------image----------------------------------
copy_field("node.preview.url", "image")

# ---------------id----------------------------------
if exists ("node.properties.ccm:wwwurl[].1")
  copy("node.properties.ccm:wwwurl[].1", "id")
  trim("id")
  replace_all("id", "\\u00a0", "") /* %A0 / &nbsp; */
else
  copy_field("node.ref.id", "id")
  append("id","https://$[service_domain]/edu-sharing/components/render/")
end

# ---------mainEntityOfPage----------------
copy_field("node.ref.id", "mainEntityOfPage[].$append.id")
append("mainEntityOfPage[].$last.id","https://$[service_domain]/edu-sharing/components/render/")
copy_field("node.modifiedAt", "mainEntityOfPage[].$last.dateModified")
replace_all("mainEntityOfPage[].$last.dateModified","T.+Z","")
copy_field("node.createdAt", "mainEntityOfPage[].$last.dateCreated")
replace_all("mainEntityOfPage[].$last.dateCreated","T.+Z","")

add_field("mainEntityOfPage[].$last.provider.id","$[service_id]")
add_field("mainEntityOfPage[].$last.provider.type","Service")
add_field("mainEntityOfPage[].$last.provider.name","$[service_name]")

# ---------------description----------------------------------
unless any_equals("node.properties.cclom:general_description[].*","")
  copy_field("node.properties.cclom:general_description[].*", "description") #eventuell nur .1 nicht .*
end

# ---------------about----------------------------------
set_array("@hochschulfaechersystematik[]")
copy_field("node.properties.ccm:taxonid[].*", "@hochschulfaechersystematik[].$append")
filter("@hochschulfaechersystematik[].*", "^https://w3id.org/kim/hochschulfaechersystematik/.*")

set_array("about[]")
copy_field("@hochschulfaechersystematik[].*", "about[].$append.id")
copy_field("@hochschulfaechersystematik[].*", "about[].$last.prefLabel.de")
lookup("about[].*.prefLabel.de", "data/maps/subject-labels.tsv","sep_char":"\t")


# ---------------contributor----------------------------------

set_array("creator[]")
do list(path:"node.properties.ccm:lifecyclecontributer_author[]", "var": "$i")
  if any_match("$i", "^BEGIN:VCARD[\\s\\S]*FN:(.+)\n.*")
    copy_field("$i", "creator[].$append.name")
    replace_all("creator[].$last.name","^BEGIN:VCARD[\\s\\S]*FN:(.+)\n.*", "${1}")
    if any_match("$i", "^BEGIN:VCARD[\\s\\S]*X-ORCID:(.+)\n.*")
      copy_field("$i", "creator[].$last.id")
      replace_all("creator[].$last.id","^BEGIN:VCARD[\\s\\S]*X-ORCID:(.+)\n.*", "${1}")
    end
    add_field("creator[].$last.type", "Person")
  elsif any_match("$i", "^BEGIN:VCARD\nORG:(.+)\n.*")
    copy_field("$i", "creator[].$append.name")
    replace_all("creator[].$last.name","^BEGIN:VCARD\nORG:(.+)\n.*", "${1}")
    add_field("creator[].$last.type", "Organization")
  end
end

# ---------------sourceOrganization----------------------------------      
set_array("sourceOrganization[]")
  copy_field("node.properties.ccm:university_DISPLAYNAME[].1", "sourceOrganization[].$append.name")
  add_field("sourceOrganization[].$last.typpe", "Organization")

# ---------------license----------------------------------
copy_field("node.properties.virtual:licenseurl[].1", "license.id")
filter("license.id","^http[s]?:\\/\\/creativecommons.org\\/(licenses|licences|publicdomain)\\/.*|^http[s]?:\\/\\/www.gnu.org\\/licenses\\/.*|^http[s]?:\\/\\/www.apache.org\\/licenses\\/.*|http[s]?://opensource.org/licenses/MIT|^http[s]?:\\/\\/www.opensource.org\\/licenses\\/BSD.*")


# ---------------inLanguage----------------------------------
set_array("inLanguage[]")
copy_field("node.properties.cclom:general_language[].*", "inLanguage[].$append")

replace_all("inLanguage[].*","_..$", "") /* remove country suffixes eg. _DE */
replace_all("inLanguage[].*","^$", "de") /* empty strings default to 'de' */
replace_all("inLanguage[].*","unknown", "de")

# ---------------type----------------------------------
set_array("type[]", "LearningResource")


# ---------------learningResourceType----------------------------------
set_array("learningResourceType[]")
do list(path:"node.properties.ccm:educationallearningresourcetype[]", "var": "$i")
  unless any_equals("$i", "|null null")
    copy_field("$i", "learningResourceType[].$append.id")
    copy_field("$i", "learningResourceType[].$last.prefLabel.de")
    copy_field("$i", "learningResourceType[].$last.prefLabel.en")
  end
end

lookup("learningResourceType[].*.prefLabel.de", "data/maps/hcrt-de-labels.tsv","sep_char":"\t")
lookup("learningResourceType[].*.prefLabel.en", "data/maps/hcrt-en-labels.tsv","sep_char":"\t")


# ---------------keywords----------------------------------
copy_field("node.properties.cclom:general_keyword[]", "keywords[]")

# ---------------encoding----------------------------------
unless exists('node.properties.ccm:wwwurl[].1')
  set_array("encoding[]")
  add_field("type", "encoding[].$append.MediaObject")
  map("node.downloadUrl", "encoding[].$last.contentUrl")
  map("node.content.hash", "encoding[].$last.sha256")
  map("node.mimetype", "encoding[].$last.encodingFormat")
  map("node.size", "encoding[].$last.contentSize")
end

retain("@context[]","id","image","name","creator[]","contributor[]","sourceOrganization[]", "license","description","dataCreated","inLanguage[]","type[]","learningResourceType[]","keywords[]","about[]", "encoding[]")
vacuum()