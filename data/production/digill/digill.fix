# Delete all empty values
vacuum()

include ("../../sharedFixes/macros.fix")

# ---------------id ----------------------------------
copy_field("@graph[].3.url", "id")

# ---------------name ----------------------------------
/* Delete the plattform name in the title of the resource */
copy_field( "@graph[].3.name", "name")
replace_all("name", " - Universit√§tsverbund digiLL", "")

# ---------------image ----------------------------------
copy_field("@graph[].2.url", "image")

# ---------------dateCreated ----------------------------------
copy_field("@graph[].3.datePublished", "dateCreated")
replace_all("dateCreated", 'T.*', '')


# ---------------inLanguage ----------------------------------
set_array("inLanguage[]")
copy_field("@graph[].3.inLanguage", "inLanguage[].$append")
replace_all("inLanguage[].*", '-.*', '')

# ------ type --- 
set_array("type[]", "LearningResource")

# --- publisher ----
set_array("publisher[]")
copy_field("@graph[].1.@id","publisher[].$append.id")
copy_field("@graph[].1.name", "publisher[].$last.name")
add_field("publisher[].$last.type", "Organization")

# ------ license --- 

/* Add BY-SA 4.0 license as digiLL is saying usual license is CC BY SA) */

add_field("license.id","https://creativecommons.org/licenses/by-sa/4.0/")

# ------ learningResourceType --- 

add_field("@hcrt","https://w3id.org/kim/hcrt/course")
# only course

set_array("learningResourceType[]")
add_field("learningResourceType[].$append.id","https://w3id.org/kim/hcrt/course")
add_field("learningResourceType[].$last.prefLabel.de","Kurs")
add_field("learningResourceType[].$last.prefLabel.en","Course")


# ---------mainEntityOfPage----------------

set_array("mainEntityOfPage[]")
copy_field("id", "mainEntityOfPage[].$append.id")
add_field("mainEntityOfPage[].$last.provider.id","$[service_id]")
add_field("mainEntityOfPage[].$last.provider.type","Service")
add_field("mainEntityOfPage[].$last.provider.name","$[service_name]")

copy_field("@graph[].3.dateModified", "mainEntityOfPage[].$last.dateModified")
replace_all("mainEntityOfPage[].*.dateModified", 'T.*', '')

copy_field("@graph[].3.datePublished", "mainEntityOfPage[].$last.dateCreated")
replace_all("mainEntityOfPage[].*.dateCreated", 'T.*', '')

# ---------------conditionsOfAccess----------------------------------
add_field("conditionsOfAccess.id","http://w3id.org/kim/conditionsOfAccess/no_login") 

# ---------------@context ----------------------------------

set_array("@context[]", "https://w3id.org/kim/amb/draft/context.jsonld")
# resource is german therefore prefer de if multiple or no inLanguage are available
if any_equal("inLanguage[]","de")
  set_hash("@context[].$append", "@language": "de")
elsif any_equal("inLanguage[]","en")
  set_hash("@context[].$append", "@language": "en")
else
  copy_field("inLanguage[].1", "@context[].$append.@language")
end
# resources without inLanguage-Values set default to de
unless exists("inLanguage[].1")
  set_hash("@context[].$append", "@language": "de")
end

# ------ tidy up --- 
include ("../../sharedFixes/retain.fix")
vacuum()
