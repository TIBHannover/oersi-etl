# --------Delete all emtpy values-------
vacuum()

do once("macrosAndMaps")
  include ("../../sharedFixes/macros.fix")
  include ("../../sharedFixes/maps.fix")
  # workflow specific maps
  put_filemap("data/maps/tuDelftSubjects2destatis.tsv","tuDelftSubjects2destatis","sep_char":"\t",key_column:"0",value_column:"1",expected_columns:"-1")
end


# Filter out any books that are not textbooks
unless any_match("html.body.div.div.1.div.div.div.div.div.*.div.*.div.ul.li.*.a.href",".*textbooks")
  reject()
end

add_array("creator[]")
add_array("inLanguage[]")
add_array("keywords[]")
add_array("publisher[]")
add_array("about[]")
add_array("encoding[]")
add_array("@hochschulfaechersystematik")
do list(path:"html.head.meta","var":"$i")


  # /* Map some of the data we have to the oersi model: */
  # ---------------id --------
  copy_field("$i.DC_Identifier_URI", "id")

  # ---------------name --------
  copy_field("$i.citation_title", "name")

  # ---------------description --------
  copy_field("$i.DC_Description", "description")

  # ---------------dateCreated --------
  copy_field("$i.DC_Date_created", "dateCreated")

  # ----------------inLanguage------------
    copy_field("$i.citation_language", "inLanguage[].$append")

  # ---------------license --------
  if any_match("$i.DC_Rights","https://creativecommons.org/.*")
    copy_field("$i.DC_Rights", "license.id")
  end

  # ------ creator --- 
  do list(path:"$i.DC_Creator_PersonalName","var":"$j")
    trim("$j")
    copy_field("$j", "creator[].$append.name")
  end


  # ------ keywords------
  trim("$i.citation_keywords")
  replace_all("$i.citation_keywords","  +", " ")
  if any_contain("$i.citation_keywords", " ")
    split_field("$i.citation_keywords"," ")
    capitalize("$i.citation_keywords.*")
    join_field("$i.citation_keywords"," ")
  end
  copy_field("$i.citation_keywords", "keywords[].$append")

  # ------ datePublished------
  copy_field("$i.citation_publication_date", "datePublished")

    # ------ publisher------
  copy_field("$i.citation_publisher", "publisher[].$append.name")

  add_field("publisher[].*.type", "Organization")

  # ------ encoding --- 

  do list(path:"$i.citation_pdf_url","var":"$j")
    copy_field("$j", "encoding[].$append.contentUrl")
    add_field("encoding[].$last.encodingFormat","application/pdf")    
    add_field("encoding[].$last.type", "MediaObject")
  end


  # ---isbn---
  if exists("$i.citation_isbn")
    copy_field("$i.citation_isbn","isbn[].$append")
  end
  replace_all("isbn[].*","-","")

  # --- doi as id ---
  if exists("$i.citation_doi")
    paste("id","~https://doi.org/","$i.citation_doi", join_char:"")
  end
end

# Adjustments for creator

add_field("creator[].*.type", "Person")

do list(path:"creator[]","var":"$j")
  do list(path:"html.body.div.div.1.div.div.div.div.div.1.div.1.div","var":"$x")
    if in("$j.name","$x.div.1.label")
      copy_field("$x.div.2.value", "$j.affiliation.name")
      copy_field("$x.span.orcid", "$j.id")
    end
  end
end

do list(path:"creator[]","var":"$j")
  if exists("$j.affiliation")
    replace_all("$j.affiliation.name","^.*(Delft University of Technology).*$","$1")
    replace_all("$j.affiliation.name","^.*[,\\|][ ]?(.*?University.*?),.*$","$1")
    replace_all("$j.affiliation.name",", The Netherlands","")
    add_field("$j.affiliation.type", "Organization")
    if any_equal("$j.affiliation.name","Delft University of Technology")
      add_field("$j.affiliation.id", "https://ror.org/02e2c7k09")
    end
  end
end

# ---sourceOrganization----

add_array("sourceOrganization[]")
add_field("sourceOrganization[].$append.name", "Delft University of Technology")
add_field("sourceOrganization[].$last.type", "Organization")
add_field("sourceOrganization[].$last.id", "https://ror.org/02e2c7k09")

  # ------ about --- 

  
do list(path:"keywords[]","var":"$j")
  copy_field("$j", "@hochschulfaechersystematik.$append")
end

copy_field("html.body.div.div.1.div.div.div.div.div.*.div.*.div.ul.li.*.value","@hochschulfaechersystematik.$append")

lookup("@hochschulfaechersystematik.*","tuDelftSubjects2destatis", delete:"true")


copy_field("@hochschulfaechersystematik.*","about[].$append.id")

do list(path: "about[]")
  copy_field("id", "prefLabel.de")
  lookup("prefLabel.de","destatis-deLabel2Uri", delete:"true")
end


# -----image-----
copy_field("html.body.div.div.1.div.div.div.div.div.2.div.1.img.src","image")

# ---------mainEntityOfPage----------------

if exists("id")
  add_array("mainEntityOfPage[]")
  copy_field("id", "mainEntityOfPage[].$append.id")
  add_field("mainEntityOfPage[].$last.provider.id","$[service_id]")
  add_field("mainEntityOfPage[].$last.provider.type","Service")
  add_field("mainEntityOfPage[].$last.provider.name","$[service_name]")
else
  print_record(destination:"data/production/tuDelftOpenTextbooks/missing_ids.json",pretty:"true")
end

copy_field("id","_id")

# ---------------conditionsOfAccess----------------------------------
add_field("conditionsOfAccess.id","http://w3id.org/kim/conditionsOfAccess/no_login") 

# ------ learningResourceType --- 
add_array("learningResourceType[]")

add_field("learningResourceType[].$append.id","https://w3id.org/kim/hcrt/textbook")
add_field("learningResourceType[].$last.prefLabel.de","Lehrbuch")
add_field("learningResourceType[].$last.prefLabel.en","Textbook")

# ------ type --- 
add_array("type[]", "LearningResource","Book")

# ---------------@context ----------------------------------

add_array("@context[]", "https://w3id.org/kim/amb/context.jsonld")
# resource is german therefore prefer de if multiple or no inLanguage are available
if any_equal("inLanguage[]","de")
  add_hash("@context[].$append", "@language": "de")
elsif any_equal("inLanguage[]","en")
  add_hash("@context[].$append", "@language": "en")
else
  copy_field("inLanguage[].1", "@context[].$append.@language")
end
# resources without inLanguage-Values set default to de
unless exists("inLanguage[].1")
  add_hash("@context[].$append", "@language": "de")
end

# ------ tidy up --- 
include ("../../sharedFixes/cleanUp.fix")
