# ---------------@context ----------------------------------

set_array("@context[]", "https://w3id.org/kim/lrmi-profile/draft/context.jsonld")
set_hash("@context[].$append", "@language": "de")

#  ------------id------------------------------------
replace_all("id", '(.*?)/lernangebot', "https://open.ruhr-uni-bochum.de/lernangebot")

#  ------------inLanguage------------------------------------
move_field("inLanguage","inLanguage[].$append")

# --------------licenses-------------
move_field("license", "license-orig")
move_field("license-orig", "license.id")

# ---------------type ----------------------------------
remove_field("type")
set_array("type[]", "LearningResource")

# ---------description---------
replace_all("description", '\\w*[^>]*?\\w*=\\"(.*?)\\">', " ")
replace_all("description", '\\w*[^>]*?\\w*=\\"(.*?)\\" \\w*=\\"(.*?)\\>', " ")
replace_all("description", '<\\w*>|</\\w*>', " ")
replace_all("description", '<br />', " ")
replace_all("description", '  ', " ")
trim("description")

# ---------sourceOrganization---------
set_array("sourceOrganization[]")
add_field("sourceOrganization[].$append.name", "Ruhr-Universit√§t Bochum")
add_field("sourceOrganization[].$last.type","Organization")

# ------------learningResourceType-----
lookup("learningResourceType", "data/maps/openRubLearningResourceTypes.tsv", "sep_char":"\t")
lookup("learningResourceType", "data/maps/hcrt-de-labels-to-uri.tsv", "sep_char":"\t")
set_array("learningResourceType[]")
copy_field("learningResourceType", "learningResourceType[].$append.id")
copy_field("learningResourceType", "learningResourceTypeDE")
move_field("learningResourceType", "learningResourceTypeEN")
lookup("learningResourceTypeDE", "data/maps/hcrt-de-labels.tsv", "sep_char":"\t")
lookup("learningResourceTypeEN", "data/maps/hcrt-en-labels.tsv", "sep_char":"\t")
move_field("learningResourceTypeDE", "learningResourceType[].$last.prefLabel.de")
move_field("learningResourceTypeEN", "learningResourceType[].$last.prefLabel.en")

# ------about----
lookup("fields", "data/maps/destatisLabels-to-uri.tsv","sep_char":"\t")
do list(path: "fields", "var": f)
    copy_field("f", "about[].$append.id")
    lookup("f", "data/maps/subject-labels.tsv","sep_char":"\t")
    copy_field("f", "about[].$last.prefLabel.de")
end
remove_field('fields')

# ---------mainEntityOfPage----------------
set_array("mainEntityOfPage[]")
copy_field("id", "mainEntityOfPage[].$append.id")
add_field("mainEntityOfPage[].$last.provider.id","$[service_id]")
add_field("mainEntityOfPage[].$last.provider.type","Service")
add_field("mainEntityOfPage[].$last.provider.name","$[service_name]")
