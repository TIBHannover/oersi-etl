
# --------Delete all emtpy values-------
vacuum()


do once("macrosAndMaps")
  include ("../../sharedFixes/macros.fix")
  include ("../../sharedFixes/maps.fix")
  # workflow specific maps
  put_filemap("data/maps/openRubLearningResourceTypes.tsv","openRubLearningResourceTypes", "sep_char":"\t")
  put_filemap("data/maps/openRubFields2Destatis.tsv","openRubFields2Destatis","sep_char":"\t")
end

#  ------------id------------------------------------
replace_all("id", '(.*?)/lernangebot', "https://open.ruhr-uni-bochum.de/lernangebot")

#  ------------inLanguage------------------------------------
add_array("inLanguage[]")
move_field("inLanguage","inLanguage[].$append")

# --------------licenses-------------
move_field("license", "license-orig")
move_field("license-orig", "license.id")

# ---------------type ----------------------------------
remove_field("type")
add_array("type[]", "LearningResource")

# ---------description---------
# delete all HTML tags
replace_all("description", '\\w*[^>]*?\\w*=\\"(.*?)\\">', " ")
replace_all("description", '\\w*[^>]*?\\w*=\\"(.*?)\\" \\w*=\\"(.*?)\\>', " ")
replace_all("description", '<\\w*>|</\\w*>', " ")
replace_all("description", '<br />', " ")
replace_all("description", '  ', " ")
trim("description")

# ---------sourceOrganization---------
add_array("sourceOrganization[]")
add_field("sourceOrganization[].$append.name", "Ruhr-Universit√§t Bochum")
add_field("sourceOrganization[].$last.type","Organization")
add_field("sourceOrganization[].$last.id","https://ror.org/04tsk2644")

# ------------learningResourceType-----


lookup("learningResourceType", "openRubLearningResourceTypes", delete:"true")
lookup("learningResourceType", "hcrt-deLabel2Uri", delete:"true")

add_array("learningResourceType[]")
copy_field("learningResourceType", "learningResourceType[].$append.id")
do list(path:"learningResourceType[]")
  copy_field("id", "prefLabel.de")
  lookup("prefLabel.de", "hcrt-deLabel2Uri", delete:"true")
  copy_field("id", "prefLabel.en")
  lookup("prefLabel.en", "hcrt-enLabel2Uri", delete:"true")
end


# ------about----
# sometimes the "fields"-element is not an array and therefore has no brackets "[]". The transformation mechanism handles this:
add_array("about[]")
unless exists("fields[]")
  add_array("fields[]")
  move_field("fields", "fields[].$append")
end
lookup("fields[].*", "openRubFields2Destatis", delete:"true")
do list(path: "fields[]", "var": "$f")
  copy_field("$f", "about[].$append.id")
  copy_field("$f", "about[].$last.prefLabel.de")
end
lookup("about[].*.prefLabel.de", "destatis-deLabel2Uri", delete:"true")
remove_field('fields[]')


# ---------------conditionsOfAccess----------------------------------
add_field("conditionsOfAccess.id","http://w3id.org/kim/conditionsOfAccess/no_login")

# ---------mainEntityOfPage----------------
add_array("mainEntityOfPage[]")
copy_field("id", "mainEntityOfPage[].$append.id")
add_field("mainEntityOfPage[].$last.provider.id","$[service_id]")
add_field("mainEntityOfPage[].$last.provider.type","Service")
add_field("mainEntityOfPage[].$last.provider.name","$[service_name]")

# ---------------@context ----------------------------------

add_array("@context[]", "https://w3id.org/kim/amb/context.jsonld")
# resource is german therefore prefer de if multiple or no inLanguage are available
if any_equal("inLanguage[]","de")
  add_hash("@context[].$append", "@language": "de")
elsif any_equal("inLanguage[]","en")
  add_hash("@context[].$append", "@language": "en")
else
  copy_field("inLanguage[].1", "@context[].$append.@language")
end
# resources without inLanguage-Values set default to de
unless exists("inLanguage[].1")
  add_hash("@context[].$append", "@language": "de")
end


# ------ tidy up --- 
include ("../../sharedFixes/cleanUp.fix")