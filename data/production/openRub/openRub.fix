
# --------Delete all emtpy values-------
vacuum()

#  ------------id------------------------------------
replace_all("id", '(.*?)/lernangebot', "https://open.ruhr-uni-bochum.de/lernangebot")

#  ------------inLanguage------------------------------------
set_array("inLanguage[]")
move_field("inLanguage","inLanguage[].$append")

# --------------licenses-------------
move_field("license", "license-orig")
move_field("license-orig", "license.id")

# ---------------type ----------------------------------
remove_field("type")
set_array("type[]", "LearningResource")

# ---------description---------
# delete all HTML tags
replace_all("description", '\\w*[^>]*?\\w*=\\"(.*?)\\">', " ")
replace_all("description", '\\w*[^>]*?\\w*=\\"(.*?)\\" \\w*=\\"(.*?)\\>', " ")
replace_all("description", '<\\w*>|</\\w*>', " ")
replace_all("description", '<br />', " ")
replace_all("description", '  ', " ")
trim("description")

# ---------sourceOrganization---------
set_array("sourceOrganization[]")
add_field("sourceOrganization[].$append.name", "Ruhr-Universit√§t Bochum")
add_field("sourceOrganization[].$last.type","Organization")

# ------------learningResourceType-----


lookup("learningResourceType", "data/maps/openRubLearningResourceTypes.tsv", "sep_char":"\t", delete:"true")
lookup("learningResourceType", "data/maps/hcrt-de-labels-to-uri.tsv", "sep_char":"\t", delete:"true")

set_array("learningResourceType[]")
copy_field("learningResourceType", "learningResourceType[].$append.id")
do list(path:"learningResourceType[]")
  copy_field("id", "prefLabel.de")
  lookup("prefLabel.de", "data/maps/hcrt-de-labels.tsv","sep_char":"\t", delete:"true")
  copy_field("id", "prefLabel.en")
  lookup("prefLabel.en", "data/maps/hcrt-en-labels.tsv","sep_char":"\t", delete:"true")
end


# ------about----
# sometimes the "fields"-element is not an array and therefore has no brackets "[]". The transformation mechanism handles this:
set_array("about[]")
unless exists("fields[]")
  set_array("fields[]")
  move_field("fields", "fields[].$append")
end
lookup("fields[].*", "data/maps/openRubFields2Destatis.tsv","sep_char":"\t", delete:"true")
do list(path: "fields[]", "var": "$f")
  copy_field("$f", "about[].$append.id")
  copy_field("$f", "about[].$last.prefLabel.de")
end
lookup("about[].*.prefLabel.de", "data/maps/subject-labels.tsv","sep_char":"\t", delete:"true")
remove_field('fields[]')


# ---------mainEntityOfPage----------------
set_array("mainEntityOfPage[]")
copy_field("id", "mainEntityOfPage[].$append.id")
add_field("mainEntityOfPage[].$last.provider.id","$[service_id]")
add_field("mainEntityOfPage[].$last.provider.type","Service")
add_field("mainEntityOfPage[].$last.provider.name","$[service_name]")

# ---------------@context ----------------------------------

set_array("@context[]", "https://w3id.org/kim/amb/draft/context.jsonld")
# resource is german therefore prefer de if multiple or no inLanguage are available
if any_equal("inLanguage[]","de")
  set_hash("@context[].$append", "@language": "de")
elsif any_equal("inLanguage[]","en")
  set_hash("@context[].$append", "@language": "en")
else
  copy_field("inLanguage[].1", "@context[].$append.@language")
end
unless exists("inLanguage[]")
  set_hash("@context[].$append", "@language": "de")
end


# ------ tidy up --- 
retain("@context[]","id","image","name","creator[]","contributor[]","sourceOrganization[]", "license","description","dateCreated","inLanguage[]","type[]","learningResourceType[]","keywords[]","about[]", "encoding[]", "publisher[]", "mainEntityOfPage[]")
vacuum()