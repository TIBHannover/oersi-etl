# Delete all empty values
vacuum()

do once("macrosAndMaps")
  include ("../../sharedFixes/macros.fix")
  include ("../../sharedFixes/maps.fix")
end

# --- reuse edu-sharing default template -------- 
include ("../../sharedFixes/eduSharingDefault.fix")

# -----id-----

remove_field("id")

# Zoerr needs a special handling of the id if collection item.
# This is combined with the special handling of series for all edu-sharing.

if exists("@external_id")
  if any_equal("properties.virtual:childobjectcount[]", "0")
    copy_field("@external_id", "id")
  elsif exists("properties.virtual:childobjectcount[]") # a series of objects. since the first conditional filters all "properties.virtual:childobjectcount[]" = "0", only the rest is kept in the second part of the conditional
    # prefer this originalId as id if collection item
    if exists("originalId")
      copy_field("originalId", "id")
      prepend("id","https://$[service_domain]/edu-sharing/components/render/")
      paste("mainEntityOfPage[].1.id","id")
    else
      copy_field("@internal_id", "id")
    end
  else
    copy_field("@external_id", "id")
  end
# prefer this originalId as id if collection item
elsif exists("originalId")
  copy_field("originalId", "id")
  prepend("id","https://$[service_domain]/edu-sharing/components/render/")
  paste("mainEntityOfPage[].1.id","id")
else
  copy_field("@internal_id", "id")
end


# ---------------datePublished----------------------------------

copy_field("properties.ccm:published_dateISO8601[].1", "datePublished")
 
# ---------------learningResourceType----------------------------------

# Youtube Videos seem to have the labels instead of the id. 
add_array("learningResourceType[]")  # add_array is desctructive and deletes existing learningResourceType-element
do list(path:"properties.ccm:educationallearningresourcetype[]", "var": "$i")
  unless any_equal("$i", "null null")
    if all_equal("$i", "Video")
      replace_all("$i", "Video", "https://w3id.org/kim/hcrt/video")
    end  
    copy_field("$i", "learningResourceType[].$append.id")
    copy_field("$i", "learningResourceType[].$last.prefLabel.de")
    copy_field("$i", "learningResourceType[].$last.prefLabel.en")
  end
end
 
lookup("learningResourceType[].*.prefLabel.de", "hcrt-deLabel2Uri", delete:"true")
lookup("learningResourceType[].*.prefLabel.en", "hcrt-enLabel2Uri", delete:"true")


include ("../../sharedFixes/cleanUp.fix")
