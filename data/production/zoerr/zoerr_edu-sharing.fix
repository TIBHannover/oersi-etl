# Delete all empty values
vacuum()

include ("../../sharedFixes/macros.fix")

# --- reuse edu-sharing default template -------- 
include ("../../sharedFixes/eduSharingDefault.fix")

#  ---------------creator----------------------------------
set_array("creator[]") # set_array is desctructive and deletes existing creator-element
do list(path:"properties.ccm:lifecyclecontributer_author[]", "var": "$i")
  if any_match("$i", "^BEGIN:VCARD[\\s\\S]*FN:(.+)\r\n[\\s\\S]*")
    copy_field("$i", "creator[].$append.name")
    add_field("creator[].$last.type", "Person")
  elsif any_match("$i", "^BEGIN:VCARD\r\nORG:(.+)\r\n[\\s\\S]*")
    copy_field("$i", "creator[].$append.name")
    add_field("creator[].$last.type", "Organization")
  end
end

do list(path:"creator[]")
  if any_match("name", "^BEGIN:VCARD[\\s\\S]*X-ORCID:https\\\\://(.+)\r\n[\\s\\S]*")
    copy_field("name", "id")
    replace_all("id","^BEGIN:VCARD[\\s\\S]*X-ORCID:https\\\\://(.+)\r\n[\\s\\S]*", "https://$1")
    replace_all("id", " ", "")
  end
  replace_all("name","^BEGIN:VCARD[\\s\\S]*FN:(.+)\r\n[\\s\\S]*", "$1")
  replace_all("name","^BEGIN:VCARD\r\nORG:(.+)\r\n[\\s\\S]*", "$1")
end

# ---------------datePublished----------------------------------

copy_field("properties.ccm:published_dateISO8601[].1", "datePublished")
 
# ---------------learningResourceType----------------------------------

# Youtube Videos seem to have the labels instead of the id. 
set_array("learningResourceType[]")  # set_array is desctructive and deletes existing learningResourceType-element
do list(path:"properties.ccm:educationallearningresourcetype[]", "var": "$i")
  unless any_equal("$i", "null null")
    if all_equal("$i", "Video")
      replace_all("$i", "Video", "https://w3id.org/kim/hcrt/video")
    end  
    copy_field("$i", "learningResourceType[].$append.id")
    copy_field("$i", "learningResourceType[].$last.prefLabel.de")
    copy_field("$i", "learningResourceType[].$last.prefLabel.en")
  end
end
 
lookup("learningResourceType[].*.prefLabel.de", "data/maps/hcrt-de-labels.tsv","sep_char":"\t", delete:"true")
lookup("learningResourceType[].*.prefLabel.en", "data/maps/hcrt-en-labels.tsv","sep_char":"\t", delete:"true")


include ("../../sharedFixes/retain.fix")
vacuum()
