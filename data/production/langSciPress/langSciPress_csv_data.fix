
# --------Delete all emtpy values-------
vacuum()

if any_equal("superseded","True")
	reject()
end

/* Map some of the data we have to the oersi model: */
# ---------------id --------
copy_field("ID", "id_csv")
prepend("id_csv","https://langsci-press.org/catalog/book/")

copy_field("id_csv", "_id")

# ------ inLanguage------
add_array("inLanguage[]")
copy_field("metalanguage", "inLanguage[].$append")

do once("languageMap")
  put_map("languageMap",
    "eng": "en",
    "fra": "fr",
    "deu": "de",
    "por": "pt",
    "spa": "es",
    "cmn": "zh"
  )
end

lookup("inLanguage[].*","languageMap")

# ---------------@context ----------------------------------

add_array("@context[]", "https://w3id.org/kim/amb/context.jsonld")
# resource is german therefore prefer de if multiple or no inLanguage are available
if any_equal("inLanguage[]","de")
  add_hash("@context[].$append", "@language": "de")
elsif any_equal("inLanguage[]","en")
  add_hash("@context[].$append", "@language": "en")
else
  copy_field("inLanguage[].1", "@context[].$append.@language")
end
# resources without inLanguage-Values set default to de
unless exists("inLanguage[].1")
  add_hash("@context[].$append", "@language": "de")
end

retain("inLanguage[]", "_id", "@context[]","DOI")
