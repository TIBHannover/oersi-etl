
# --------Delete all emtpy values-------
vacuum()

# Rejects records of superseded books. That are not filtered out in the csv.
if any_contain("html.head.meta.DC_Description","superseded")
  reject()
end

do list(path:"html.head.meta","var":"$i")

  /* Map some of the data we have to the oersi model: */
  # ---------------id --------
  copy_field("$i.DC_Identifier_URI", "id")

  # ---------------name --------
  copy_field("$i.citation_title", "name")


  # ---------------description --------
  copy_field("$i.DC_Description", "description")


  # ---------------dateCreated --------
  copy_field("$i.DC_Date_created", "dateCreated")

  # ---------------license --------
  if any_match("$i.DC_Rights","https://creativecommons.org/.*")
    copy_field("$i.DC_Rights", "license.id")
  end

end

# ------ creator --- 
  add_array("creator[]")
do list(path:"html.head.meta.DC_Creator_PersonalName","var":"$i")

  copy_field("$i", "creator[].$append.name")
end
add_field("creator[].*.type", "Person")

# ------ keywords------
  add_array("keywords[]")
do list(path:"html.head.meta.citation_keywords","var":"$i")
  copy_field("$i", "keywords[].$append")
end

# ------ datePublished------
do list(path:"html.head.meta.citation_publication_date","var":"$i")

  copy_field("$i", "datePublished")
end

  # ------ publisher------
  add_array("publisher[]")
do list(path:"html.head.meta.citation_publisher","var":"$i")

  copy_field("$i", "publisher[].$append.name")
end

add_field("publisher[].*.type", "Organization")

# ------ encoding --- 

add_array("encoding[]")
do list(path:"html.head.meta.citation_pdf_url","var":"$i")
  copy_field("$i", "encoding[].$append.contentUrl")
  add_field("encoding[].$last.encodingFormat","application/pdf")    
  add_field("encoding[].$last.type", "MediaObject")
end

# ---------------conditionsOfAccess----------------------------------
add_field("conditionsOfAccess.id","http://w3id.org/kim/conditionsOfAccess/no_login") 

# ------ learningResourceType --- 
add_array("learningResourceType[]")

add_field("learningResourceType[].$append.id","https://w3id.org/kim/hcrt/textbook")
add_field("learningResourceType[].$last.prefLabel.de","Lehrbuch")
add_field("learningResourceType[].$last.prefLabel.en","Textbook")

# ------ type --- 
add_array("type[]", "LearningResource")

# ------ about --- 
add_array("about[]")
add_field("about[].$append.id", "https://w3id.org/kim/hochschulfaechersystematik/n07")
add_field("about[].$last.prefLabel.de", "Allgemeine und vergleichende Literatur- und Sprachwissenschaft")




# ---------mainEntityOfPage----------------

if exists("id")
  add_array("mainEntityOfPage[]")
  copy_field("id", "mainEntityOfPage[].$append.id")
  add_field("mainEntityOfPage[].$last.provider.id","$[service_id]")
  add_field("mainEntityOfPage[].$last.provider.type","Service")
  add_field("mainEntityOfPage[].$last.provider.name","$[service_name]")
else
  print_record(destination:"data/production/langSciPress/missing_ids.json",pretty:"true")
end

copy_field("id","_id")

# ------ tidy up --- 
retain("_id","id","image","name","creator[]","contributor[]","sourceOrganization[]", "license","description","datePublished","dateCreated","dateModified","inLanguage[]","type[]","learningResourceType[]","keywords[]","about[]","teaches[]","assesses[]","competencyRequired[]","educationalLevel[]", "encoding[]", "publisher[]", "isPartOf[]", "mainEntityOfPage[]","conditionsOfAccess","duration")
vacuum()
