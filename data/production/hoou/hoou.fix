vacuum()

# Following fields are provided:
# id
# name
# image
# description


# ---------------@context ----------------------------------

set_array("@context[]", "https://w3id.org/kim/lrmi-profile/draft/context.jsonld")
set_hash("@context[].$append", "@language": "de")


# ---------------license ----------------------------------
move_field("license", "@license")
copy_field("@license", "license.id")

# ---------------dateCreated----------------------------------
replace_all("dateCreated","T.*", "")

# ---------------datePublished----------------------------------
replace_all("datePublished","T.*", "")

# ---------------dateModified ----------------------------------
replace_all("dateModified","T.*", "")


# ---------------creator ----------------------------------
copy_field("author[]","creator[]")

replace_all("creator[].*.name", "Apl\\. |\\(apl\\.\\) |Dr\\. med |Dr\\. |Dr\\.'in |Dr |Dr\\.med\\.|Dres\\. |Dr\\.|Prof\\. |Prof\\.'in |Prof\\.|Prof |Jun\\.-|PD |PD\\. |med\\. |rer\\. |pol\\.|nat\\. |dent\\. |em\\. |Ao\\.-|o\\. Univ\\.|Uni\\.-|Univ\\.-|Univ\\. |DI |RA |Dipl\\.|-Ing\\. |-Inform\\. |-Psych\\. |h\\.c\\. |mult\\. |Mag\\. |, MME|; MME|, MScN|M\\. Sc\\. |, MSc| MSc\\.|, M\\.A\\.| M\\.A\\.|, M\\.D\\.|, B\\.A\\.|, MMZ|, Psychoanalytiker|, L\\.L\\.M\\.|, LL\\.M\\.| \\(M\\.A\\.\\)", "")


# ---------------sourceOrganization ----------------------------------

replace_all("provider[].*.type",".*","Organization")
copy_field("provider[]","sourceOrganization[]")

# ---------------keywords----------------------------------
split_field("keywords",",")
trim("keywords.*")
move_field("keywords","keywords[]")

# ---------------about ----------------------------------
set_array("@about")
do list(path:"keywords[]", "var":"$i")
  copy_field("$i","@about.$append")
end

set_array("about[]")
do list(path:"@about", "var":"$i")
  lookup("$i", "data/maps/hoou-subject-mapping.tsv","sep_char":"\t")
  lookup("$i", "data/maps/subject-url-mapping.tsv","sep_char":"\t")
  copy_field("$i", "about[].$append.id")
  copy_field("$i", "about[].$last.prefLabel.de")
end
lookup("about[].*.prefLabel.de", "data/maps/subject-labels.tsv","sep_char":"\t")


# ---------------type----------------------------------
set_array("type[]", "LearningResource")

# ---------------learningResourceType----------------------------------

/* HOOU provides at the moment only Course or CreativeWork as types. Also learningResourceType is set with Video oder Bilder but not very often. */
set_array("@hcrt")
copy_field("@type|learningResourceType", "@hcrt.$append")
replace_all("@hcrt.*","Bilder","Image")
lookup("@hcrt.*","data/maps/en-labels-hcrt-uri.tsv","sep_char":"\t")
copy_field("@hcrt.*", "learningResourceType[].$append.id")
set_array("learningResourceType[]")
do list(path:"learningResourceType[]")
  copy_field("id", "prefLabel.de")
  lookup("prefLabel.de", "data/maps/hcrt-de-labels.tsv","sep_char":"\t")
  copy_field("id", "prefLabel.en")
  lookup("prefLabel.en", "data/maps/hcrt-en-labels.tsv","sep_char":"\t")
end


# ---------mainEntityOfPage----------------
set_array("mainEntityOfPage[]")
copy_field("id", "mainEntityOfPage[].$append.id")
add_field("mainEntityOfPage[].$last.provider.id","$[service_id]")
add_field("mainEntityOfPage[].$last.provider.type","Service")
add_field("mainEntityOfPage[].$last.provider.name","$[service_name]")

# ------ tidy up --- 
retain("@context[]","id","image","name","creator[]","contributor[]","sourceOrganization[]", "license","description","datePublished","dateCreated","dateModified", "inLanguage[]","type[]","learningResourceType[]","keywords[]","about[]", "encoding[]", "mainEntityOfPage[]")
vacuum()
