vacuum()

# Following fields are provided:
# id
# name
# image
# audience
# description
# sourceOrganization


do once("macrosAndMaps")
  include ("../../sharedFixes/macros.fix")
  include ("../../sharedFixes/maps.fix")
  # workflow specific map
  put_filemap("data/maps/hoou-subject-mapping.tsv","hoou-subject-mapping","sep_char":"\t")
  put_filemap("data/maps/hoou-audience-mapping.tsv","hoou-audience-mapping","sep_char":"\t")
  put_rdfmap("data/maps/vocabulary/educationalAudienceRole.ttl", "audienceDeLabel", target: "skos:prefLabel", select_language: "de")
  put_rdfmap("data/maps/vocabulary/educationalAudienceRole.ttl", "audienceEnLabel", target: "skos:prefLabel", select_language: "en-US")
end

# ---------------license ----------------------------------
move_field("license", "@license")
copy_field("@license", "license.id")

# ---------------dateCreated----------------------------------
replace_all("dateCreated","T.*", "")

# ---------------datePublished----------------------------------
replace_all("datePublished","T.*", "")

# ---------------dateModified ----------------------------------
replace_all("dateModified","T.*", "")


# ---------------creator ----------------------------------
copy_field("author[]","creator[]")
call_macro("deleteAcademicTitles", field: "creator[].*.name")

# ---------------sourceOrganization ----------------------------------

replace_all("sourceOrganization[].*.type","^.*$","Organization")

# ---------------keywords----------------------------------
split_field("keywords",",")
trim("keywords.*")
move_field("keywords","keywords[]")

# ---------------about ----------------------------------
set_array("@about")
do list(path:"keywords[]", "var":"$i")
  copy_field("$i","@about.$append")
end

set_array("about[]")
do list(path:"@about", "var":"$i")
  lookup("$i", "hoou-subject-mapping", delete:"true")
  copy_field("$i", "about[].$append.id")
  copy_field("$i", "about[].$last.prefLabel.de")
end
lookup("about[].*.prefLabel.de", "destatis-deLabel2Uri", delete:"true")


# ---------------type----------------------------------
set_array("type[]", "LearningResource")

# ---------------learningResourceType----------------------------------

/* HOOU provides at the moment only Course or CreativeWork as types. Also learningResourceType is set with Video oder Bilder but not very often. */
set_array("@hcrt")
set_array("learningResourceType[]")
copy_field("type|learningResourceType", "@hcrt.$append")
replace_all("@hcrt.*","Bilder","Image")
lookup("@hcrt.*","hcrt-enLabel2Uri", delete:"true")

copy_field("@hcrt.*", "learningResourceType[].$append.id")

do list(path:"learningResourceType[]")
  copy_field("id", "prefLabel.de")
  lookup("prefLabel.de", "hcrt-deLabel2Uri", delete:"true")
  copy_field("id", "prefLabel.en")
  lookup("prefLabel.en", "hcrt-enLabel2Uri", delete:"true")
end

# ---------------conditionsOfAccess----------------------------------
add_field("conditionsOfAccess.id","http://w3id.org/kim/conditionsOfAccess/no_login")


# ---------mainEntityOfPage----------------
replace_all("id", 'legacy.hoou.de', 'www.hoou.de')
set_array("mainEntityOfPage[]")
copy_field("id", "mainEntityOfPage[].$append.id")
add_field("mainEntityOfPage[].$last.provider.id","$[service_id]")
add_field("mainEntityOfPage[].$last.provider.type","Service")
add_field("mainEntityOfPage[].$last.provider.name","$[service_name]")

# ---------------@context ----------------------------------

set_array("@context[]", "https://w3id.org/kim/amb/context.jsonld")
# resource is german therefore prefer de if multiple or no inLanguage are available
if any_equal("inLanguage[]","de")
  set_hash("@context[].$append", "@language": "de")
elsif any_equal("inLanguage[]","en")
  set_hash("@context[].$append", "@language": "en")
else
  copy_field("inLanguage[].1", "@context[].$append.@language")
end
# resources without inLanguage-Values set default to de
unless exists("inLanguage[].1")
  set_hash("@context[].$append", "@language": "de")
end

# ---------audience----------------
copy_field("audience[]", "@tempAudience[]")
set_array("audience[]")

do list(path:"@tempAudience[]", "var": "$i")
  # Replace ID with mappings and delete values not known
  lookup("$i.educationalRole", "hoou-audience-mapping", delete:"true")
  unless is_empty("$i.educationalRole")
    copy_field("$i.educationalRole", "audience[].$append.id")
    copy_field("$i.educationalRole", "audience[].$last.prefLabel.de")
    copy_field("$i.educationalRole", "audience[].$last.prefLabel.en")
  end
end

lookup("audience[].*.prefLabel.de", "audienceDeLabel")
lookup("audience[].*.prefLabel.en", "audienceEnLabel")

# ------ tidy up ---
include ("../../sharedFixes/cleanUp.fix")
