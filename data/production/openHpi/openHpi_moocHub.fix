# Filter out all resources that are not free

unless any_match("attributes.access[].","free")
  reject()
end

# Delete all empty values
vacuum()

include ("../../sharedFixes/macros.fix")
include ("../../sharedFixes/moocHubDefault.fix")


#  ---------------description----------------------------------
copy_field("attributes.description", "description")
replace_all("description",'<a[^>]*?href=\\"(.*?)\\">', "")
replace_all("description","<\\w*>|</\\w*>", "")

#  ---------------creator----------------------------------
# source data is not consistent, can be a single author or multiple separated by comma or "&amp;". API estimates one object per instructor. Also there is no type even though there are persons and organisations.*/
set_array("@creator")
copy_field("attributes.instructors[].*.name", "@creator.$append")
call_macro("deleteAcademicTitles", field: "@creator.*")


set_array("creator[]")
do list(path:"@creator", "var": "$i")
  copy_field("$i","creator[].$append.name")
  add_field("creator[].$last.type", "Person")
end

# ------ sourceOrganization --- 

set_array("sourceOrganization[]")
do list(path:"attributes.partnerInstitute[]", "var": "$i")
  copy_field("$i.name", "sourceOrganization[].$append.name")
  add_field("sourceOrganization[].$last.type", "Organization")
end


add_field("sourceOrganization[].$append.name", "Hasso-Plattner-Institut")
add_field("sourceOrganization[].$last.type", "Organization")
add_field("sourceOrganization[].$last.id", "https://ror.org/058rn5r42")

# ---------------conditionsOfAccess----------------------------------
add_field("conditionsOfAccess.id","http://w3id.org/kim/conditionsOfAccess/no_login") 

#---------Trailer----------

copy_field("attributes.video.url","trailer.embedUrl")
if exists("trailer.embedUrl")
	add_field("trailer.type","VideoObject")
end

# ------ tidy up --- 
include ("../../sharedFixes/cleanUp.fix")

/*missing:*/
/*kewords*/
/*about*/