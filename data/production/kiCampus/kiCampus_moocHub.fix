# Filter out all resources that are not free

unless any_match("attributes.access[].","free")
  reject()
end

# Delete all empty values
vacuum()


include ("../../sharedFixes/macros.fix")
include ("../../sharedFixes/moocHubDefault.fix")

#  ---------------description----------------------------------
copy_field("attributes.description", "description")
replace_all("description",'<a[^>]*?href=\\"(.*?)\\">', "")
replace_all("description","<\\w*>|</\\w*>", "")

#  ---------------creator----------------------------------
# source data is not consistent, can be a single author or multiple separated by comma or "&amp;". API estimates one object per instructor. Also there is no type even though there are persons and organisations.*/
set_array("@creator")
copy_field("attributes.instructors[].*.name", "@creator.$append")
call_macro("deleteAcademicTitles", field: "@creator.*")
replace_all("$i","^ $","")
trim("@creator.*")

copy_field("attributes.courseLicenses[].*.author", "@author")
trim("@author")
split_field("@author",", ")
do list(path: "@author", "var":"$i")
  call_macro("deleteAcademicTitles", field: "$i")
  replace_all("$i","^ $","")
  unless in("$i","@creator")
    copy_field("$i", "@creator.$append")
  end
end
vacuum()

set_array("creator[]")
do list(path:"@creator", "var": "$i")
  copy_field("$i","creator[].$append.name")
  add_field("creator[].$last.type", "Person")
end

# ------ sourceOrganization --- 

set_array("sourceOrganization[]")
# sourceOrganization Should OpenHPI always be the sourceOrganization?

do list(path:"attributes.partnerInstitute[]", "var": "$i")
  copy_field("$i.name", "sourceOrganization[].$append.name")
  add_field("sourceOrganization[].$last.type", "Organization")
end


# add_field("sourceOrganization[].$append.name", "Hasso-Plattner-Institut")
# add_field("sourceOrganization[].$last.type", "Organization")

# ------ about --- 
set_array("about[]")
add_field("about[].$append.id", "https://w3id.org/kim/hochschulfaechersystematik/n71")
add_field("about[].$last.prefLabel.de", "Studienbereich Informatik")

# ---------------conditionsOfAccess----------------------------------
add_field("conditionsOfAccess.id","http://w3id.org/kim/conditionsOfAccess/login") 



# ------ tidy up --- 
include ("../../sharedFixes/cleanUp.fix")

/*missing:*/
/*kewords*/
/*about*/