/* Set up the context */
do array("@context")
 add_field("","https://w3id.org/kim/lrmi-profile/draft/context.jsonld")
 do entity("")
  add_field("@language", "de")
 end
end

/* Mapping the data to element level: */
do map("metadata.mods.identifier.value", "id")
  regexp(match:"^https://duepublico2.uni-due.de/receive/.*")
end

/* To map the image the location.url needs to carry the attribute access preview. Could creat confusion:*/
do map("metadata.mods.location.url.value", "image")
  regexp(match:"^https://duepublico2.uni-due.de/rsc/thumbnail/.*")
end

do choose(flushWith: record)
  do combine(name: "name", value:  "${pre} ${title}")
      map("metadata.mods.titleInfo.title.value", "title")
      map("metadata.mods.relatedItem.titleInfo.nonSort.value","pre")
  end
  map("metadata.mods.titleInfo.title.value", "name")
end

do array("creator", flushWith: record)
  do entity("", sameEntity:"true")
    do choose(flushWith: "metadata.mods.name")
      do map("metadata.mods.name.type", "type")
        equals(string: "personal")
        constant(value:"Person")
      end
      do map("metadata.mods.name.type", "type")
        equals(string: "corporate")
        constant(value:"Organization")
      end
    end  
    do choose(flushWith: "metadata.mods.name")
      do map("metadata.mods.name.valueURI", "name")
        lookup(in: "data/maps/duepub-corporate.tsv")
      end
      do map("metadata.mods.name.displayForm.value", "name")
        replace(pattern: "(.+), (.+)", with: "$2 $1")
      end
    end  
  end
end

map("metadata.mods.abstract.value", "description")

do map("metadata.mods.accessCondition.href", "license")
  lookup(in: "data/maps/DuePub-Licence.tsv")
end

map("metadata.mods.originInfo.dateIssued.value", "dataCreated")

do array("inLanguage")
  map("metadata.mods.language.languageTerm.value")
end

do array ("type")
  add_field("","LearningResource")
end

do map('metadata.mods.genre.value', '@hcrt')
  lookup(in: 'data/maps/DuePub-HCRT.tsv')
end
do array('learningResourceType')
  do entity('')
    map('@hcrt', 'id')
    do entity('prefLabel')
      do map('@hcrt', 'de')
        lookup(in: 'data/maps/hcrt-de-labels.tsv')
      end  
      do map('@hcrt', 'en')
        lookup(in: 'data/maps/hcrt-en-labels.tsv')
      end
    end
  end
end

do array("keywords")
  map("metadata.mods.subject.topic.value")
end


do map("metadata.mods.classification.valueURI","@hochschulfaechersystematik")
  lookup(in: "data/maps/DestatisPersonal2DestatisStudierende.tsv")
  unique()
end
  
do array("about", flushWith: record)
  do entity("", flushWith: "@hochschulfaechersystematik")
    map("@hochschulfaechersystematik", "id")
    do map("@hochschulfaechersystematik", "prefLabel.de")
      lookup(in: "data/maps/subject-labels.tsv")
    end
  end
end

/* mainEntityofPage */

do array("mainEntityOfPage")
  do entity("")
    do map("metadata.mods.identifier.value", "id")
      regexp(match:"^https://duepublico2.uni-due.de/receive/.*")
    end
    /* Add provider/source information to each resource description */
    do entity("provider")
      add_field("id","$[service_id]")
      add_field("type","Service")
      add_field("name","$[service_name]")
    end
  end
end

/* map(_else) */

/* Fehlen noch: */
/* map(metadata.mods. , publisher) Who is the publisher? The Resource or only the real publisher? */
/* map(metadata.mods. , audience) DuePublico is missing audience */
/* map(metadata.mods. , isBasedOn) */