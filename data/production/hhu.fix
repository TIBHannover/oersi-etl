/* Set up the context */
do array("@context")
 add_field("","https://w3id.org/kim/lrmi-profile/draft/context.jsonld")
 do entity("")
  add_field("@language", "de")
 end
end

/* Map some of the data we have to the oersi model: */
map("html.head.meta.og:title", "name")
map("html.head.meta.og:description", "description")
map("html.head.meta.og:image","image")

/* Creation-Date comes in as "- recorded at 9/19/2012" */
do map("*.span.watch-information-date produced", "dateCreated")
  regexp(match: "- recorded at \\d{1,2}}/\\d{1,2}}/\\d{4}")
  replace(pattern: "(\\d)/(\\d+)/(\\d\\d\\d\\d)", with: "0$1/$2/$3")
  replace(pattern: "(\\d+)/(\\d)/(\\d\\d\\d\\d)", with: "$1/0$2/$3")
  replace(pattern: "- recorded at (\\d\\d)/(\\d\\d)/(\\d\\d\\d\\d)", with: "$3-$1-$2")
end

/*rel = "liscence" cant be directly mapped */

do entity("license")
  do map("*.a.license", "id")
        regexp(match: '^http[s]?:\\/\\/creativecommons.org\\/(licenses|licences|publicdomain)\\/.*|^http[s]?:\\/\\/www.gnu.org\\/licenses\\/.*|^http[s]?:\\/\\/www.apache.org\\/licenses\\/.*|http[s]?://opensource.org/licenses/MIT|^http[s]?:\\/\\/www.opensource.org\\/licenses\\/BSD.*')
  end
end

do array("creator", flushWith: record)
 do entity("")
  add_field("type", "Person")
  do map("*.p.watch-speaker", "name")
    replace(pattern: "Apl\\. |\\(apl\\.\\) |Dr\\. med |Dr\\. |Dr\\.'in |Dr |Dr\\.med\\.|Dres\\. |Dr\\.|Prof\\. |Prof\\.'in |Prof\\.|Prof |Jun\\.-|PD |PD\\. |med\\. |rer\\. |pol\\.|nat\\. |dent\\. |em\\. |Ao\\.-|o\\. Univ\\.|Uni\\.-|Univ\\.-|Univ\\. |DI |RA |Dipl\\.|-Ing\\. |-Inform\\. |-Psych\\. |h\\.c\\. |mult\\. |Mag\\. |, MME|; MME|, MScN|M\\. Sc\\. |, MSc| MSc\\.|, M\\.A\\.| M\\.A\\.|, M\\.D\\.|, B\\.A\\.|, MMZ|, Psychoanalytiker|, L\\.L\\.M\\.|, LL\\.M\\.| \\(M\\.A\\.\\)", with: "")
  end 
 end
end

do array("sourceOrganization", flushWith: record)
 do entity("")
  add_field("type", "Organization")
  add_field("name", "HHU DÃ¼sseldorf")
 end
end

map("html.head.meta.og:url", "id")

do array ("type")
  add_field("","LearningResource")
end

/* Add Video always as lRT */

add_field("@hcrt","https://w3id.org/kim/hcrt/video")

do array('learningResourceType')
  do entity('')
    map('@hcrt', 'id')
    do entity('prefLabel')
      do map('@hcrt', 'de')
        lookup(in: 'data/maps/hcrt-de-labels.tsv')
      end  
      do map('@hcrt', 'en')
        lookup(in: 'data/maps/hcrt-en-labels.tsv')
      end
    end
  end
end

/* encoding: So far only embedUrl */
do map("html.body.form.div.div.div.div.div.div.div.div.div.div.div.textarea.value","@embedUrl")
  regexp(match: "^<iframe.*src='(https:\\/\\/mediathek\\.hhu\\.de\\/embed\\/.*)(' frameborder='0' allowfullscreen><\\/iframe>)$", format: '${1}')
end

do array("encoding", flushWith: record)
  do entity("")
    map("@embedUrl","embedUrl")
    do map("@embedUrl", "type")
      constant(value: "MediaObject")
    end
  end
end

do array("keywords", flushWith: record)
  map("html.body.form.div.div.div.div.div.div.div.div.div.div.div.ul.li.a.value")
end

do array("mainEntityOfPage")
  do entity("")
    do map("html.body.form.action", "id")
      replace(pattern: "./", with: "https://mediathek.hhu.de/watch/")
    end
    /* Add provider/source information to each resource description */
    do entity("provider")
      add_field("id","$[service_id]")
      add_field("type","Service")
      add_field("name","$[service_name]")
    end
  end
end