vacuum()

# ---------------@context ----------------------------------
set_array("@context[]", "https://w3id.org/kim/amb/draft/context.jsonld")
set_hash("@context[].$append", "@language": "de")

# ---------------id ----------------------------------
copy_field("html.head.meta.*.og:url", "id")

# ---------------name----------------------------------
copy_field("html.head.meta.*.og:title", "name")

# ---------------description----------------------------------
copy_field("html.head.meta.*.og:description", "description")

# ---------------image----------------------------------
copy_field("html.head.meta.*.og:image","image")

# ---------------dateCreated----------------------------------
# /* Creation-Date comes in as "- recorded at 9/19/2012" */
do list_as($date: "html.body.form.div.div.2.div.div.div.div.3.div.div.1.div.1.div.div.2.span.*.watch-information-date produced")
  replace_all($date, "(\\d)/(\\d+)/(\\d{4})", "0$1/$2/$3")
  replace_all($date, "(\\d+)/(\\d)/(\\d{4})", "$1/0$2/$3")
  replace_all($date, "- recorded at (\\d{2})/(\\d{2})/(\\d{4})", "$3-$1-$2")
  if any_match($date, "\\d{4}-\\d{2}-\\d{2}")
    move_field($date, "dateCreated")
  end
end

# ---------------license----------------------------------
do list_as($license: "html.body.form.div.div.2.div.div.div.div.3.div.div.1.div.3.div.1.div.4.div.p.a.*.license")
  if any_match($license, "^http[s]?:\\/\\/creativecommons.org\\/(licenses|licences|publicdomain)\\/.*|^http[s]?:\\/\\/www.gnu.org\\/licenses\\/.*|^http[s]?:\\/\\/www.apache.org\\/licenses\\/.*|http[s]?://opensource.org/licenses/MIT|^http[s]?:\\/\\/www.opensource.org\\/licenses\\/BSD.*")
    move_field($license, "license.id")
  end
end

# ---------------creator----------------------------------
set_array("creator[]")
do list_as($speaker: "html.body.form.div.div.2.div.div.div.div.3.div.div.1.div.3.div.1.div.*.p.watch-speaker")
  if none_equal($speaker, "no speaker")
    replace_all($speaker, "Apl\\. |\\(apl\\.\\) |Dr\\. med |Dr\\. |Dr\\.'in |Dr |Dr\\.med\\.|Dres\\. |Dr\\.|Prof\\. |Prof\\.'in |Prof\\.|Prof |Jun\\.-|PD |PD\\. |med\\. |rer\\. |pol\\.|nat\\. |dent\\. |em\\. |Ao\\.-|o\\. Univ\\.|Uni\\.-|Univ\\.-|Univ\\. |DI |RA |Dipl\\.|-Ing\\. |-Inform\\. |-Psych\\. |h\\.c\\. |mult\\. |Mag\\. |, MME|; MME|, MScN|M\\. Sc\\. |, MSc| MSc\\.|, M\\.A\\.| M\\.A\\.|, M\\.D\\.|, B\\.A\\.|, MMZ|, Psychoanalytiker|, L\\.L\\.M\\.|, LL\\.M\\.| \\(M\\.A\\.\\)", "")
    move_field($speaker, "creator[].$append.name")
    add_field("creator[].$last.type", "Person")
  end
end

# ---------------sourceOrganization----------------------------------
set_array("sourceOrganization[]")
add_field("sourceOrganization[].$append.type", "Organization")
add_field("sourceOrganization[].$last.name", "HHU DÃ¼sseldorf")

# ------ type --- 
set_array("type[]", "LearningResource")

# ------ learningResourceType --- 
# only video
set_array("learningResourceType[]")
add_field("learningResourceType[].$append.id","https://w3id.org/kim/hcrt/video")
add_field("learningResourceType[].$last.prefLabel.de","Video")
add_field("learningResourceType[].$last.prefLabel.en","Video")

# ------ encoding --- 
set_array("encoding[]")
do list_as($iframe: "html.body.form.div.div.2.div.div.div.div.3.div.div.1.div.3.div.*.div.textarea.value")
  if any_match($iframe, "^<iframe.*src='(https:\\/\\/mediathek\\.hhu\\.de\\/embed\\/.*)(' frameborder='0' allowfullscreen><\\/iframe>)$")
    replace_all($iframe, "^<iframe.*src='(https:\\/\\/mediathek\\.hhu\\.de\\/embed\\/.*)(' frameborder='0' allowfullscreen><\\/iframe>)$", "$1")
    copy_field($iframe, "encoding[].$append.embedUrl")
    add_field("encoding[].$last.type", "MediaObject")
  end
end

# ---------mainEntityOfPage----------------
set_array("mainEntityOfPage[]")
copy_field("id", "mainEntityOfPage[].$append.id")
add_field("mainEntityOfPage[].$last.provider.id","$[service_id]")
add_field("mainEntityOfPage[].$last.provider.type","Service")
add_field("mainEntityOfPage[].$last.provider.name","$[service_name]")

# --------- conditionsOfAccess ---------------
add_field("conditionsOfAccess.id","http://w3id.org/kim/conditionsOfAccess/no_login")

# --------- keywords ----------------
set_array("keywords[]")
do list_as($keyword: "html.body.form.div.div.2.div.div.div.div.3.div.div.1.div.3.div.1.div.4.ul.li.*.value")
  copy_field($keyword, "keywords[].$append")
end

# ------ tidy up --- 
retain("@context[]","id","image","name","creator[]","contributor[]","sourceOrganization[]", "license","description","dateCreated","inLanguage[]","type[]","learningResourceType[]","keywords[]","about[]", "encoding[]", "mainEntityOfPage[]")
vacuum()
