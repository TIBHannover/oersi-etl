/* Set up the context */
do array("@context")
 add_field("","https://w3id.org/kim/lrmi-profile/draft/context.jsonld")
 do entity("")
  add_field("@language", "de")
 end
end

/* Map some of the data we have to the oersi model: */
map("html.head.meta.og:title", "name")

map("html.head.meta.og:description", "description")

map("html.head.meta.og:image","image")

/*rel = "liscence" cant be directly mapped */
do entity("license")
  do map("*.a.license", "id")
    regexp(match: '^http[s]?:\\/\\/creativecommons.org\\/(licenses|licences|publicdomain)\\/.*|^http[s]?:\\/\\/www.gnu.org\\/licenses\\/.*|^http[s]?:\\/\\/www.apache.org\\/licenses\\/.*|http[s]?://opensource.org/licenses/MIT|^http[s]?:\\/\\/www.opensource.org\\/licenses\\/BSD.*')
  end
end


do array("creator", flushWith: record)
 do entity("")
  add_field("type", "Person")
  do map("html.body.div.div.div.div.section.section.section.div.h4.value", "name")
    replace(pattern: "Apl\\. |\\(apl\\.\\) |Dr\\. med |Dr\\. |Dr\\.'in |Dr |Dr\\.med\\.|Dres\\. |Dr\\.|Prof\\. |Prof\\.'in |Prof\\.|Prof |Jun\\.-|PD |PD\\. |med\\. |rer\\. |pol\\.|nat\\. |dent\\. |em\\. |Ao\\.-|o\\. Univ\\.|Uni\\.-|Univ\\.-|Univ\\. |DI |RA |Dipl\\.|-Ing\\. |-Inform\\. |-Psych\\. |h\\.c\\. |mult\\. |Mag\\. |, MME|; MME|, MScN|M\\. Sc\\. |, MSc| MSc\\.|, M\\.A\\.| M\\.A\\.|, M\\.D\\.|, B\\.A\\.|, MMZ|, Psychoanalytiker|, L\\.L\\.M\\.|, LL\\.M\\.| \\(M\\.A\\.\\)", with: "")
  end
 end
end

do array("inLanguage")
  do map("html.body.div.div.div.div.section.div.div.div.div.div.div.span.value")
    regexp(match: "Deutsch|English")
    replace(pattern: "Deutsch", with: "de")
    replace(pattern: "English", with: "en")
  end
end

map("html.head.meta.og:url", "id")

do array ("type")
  add_field("","LearningResource")
end

/* Add Course always as lRT */

add_field("@hcrt","https://w3id.org/kim/hcrt/course")

do array('learningResourceType')
  do entity('')
    map('@hcrt', 'id')
    do entity('prefLabel')
      do map('@hcrt', 'de')
        lookup(in: 'data/maps/hcrt-de-labels.tsv')
      end  
      do map('@hcrt', 'en')
        lookup(in: 'data/maps/hcrt-en-labels.tsv')
      end
    end
  end
end

do array("keywords", flushWith: record)
  map("html.body.div.div.div.div.section.section.div.a.value")
end

do array("mainEntityOfPage")
  do entity("")
    do map("html.head.meta.og:url", "id")
      replace(pattern: "\\?lang=fa", with: "")
    end
    do entity("provider")
      add_field("id","$[service_id]")
      add_field("type","Service")
      add_field("name","$[service_name]")
    end
  end
end