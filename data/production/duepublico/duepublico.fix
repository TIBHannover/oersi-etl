# See https://github.com/TobiasNx/Catmandu-Testing/blob/master/DuePub-Test/duepub-catmandu.fix

if exists ("metadata.error")
  reject()
end

if any_equal ("header.status", "deleted")
  reject()
end

# Delete all empty values
vacuum()

include ("../../sharedFixes/macros.fix")

# --------------id ----------------------------------------
do list("path": "metadata.mods:mods.mods:identifier", "var": "$i")
  if all_match("$i.value","https://duepublico2.uni-due.de/receive/.*")
    copy_field("$i.value","id")
  end
end

#  ------------image ------------------------------------
do list("path": "metadata.mods:mods.mods:location.mods:url", "var": "$i")
  if all_match("$i.value","^https://duepublico2.uni-due.de/rsc/thumbnail/.*")
    copy_field("$i.value","image")
  end
end


#  ------------encoding ------------------------------------
set_array("encoding[]")
do list("path": "metadata.mods:mods.mods:location.mods:url", "var": "$i")
  if all_match("$i.value","^https://duepublico2.uni-due.de/servlets/.*")
    copy_field("$i.value","encoding[].$append.contentUrl")
    add_field("encoding[].$last.type", "MediaObject")
  end
end

#  ------------name ------------------------------------
paste("name","metadata.mods:mods.mods:titleInfo.mods:nonSort.value","metadata.mods:mods.mods:titleInfo.mods:title.value")

#  ------------creator------------------------------------
set_array("creator[]")
do list("path":"metadata.mods:mods.mods:name","var":"$i")
  if any_match("$i.mods:role.mods:roleTerm.*.value","aut|cre")
    copy_field("$i.mods:displayForm.value","creator[].$append.name")
    if any_equal("$i.type","personal")
      add_field("creator[].$last.type", "Person")
    end
    if any_equal("$i.type","corporate")
      add_field("creator[].$last.type", "Organization")
    end
    do list(path:"$i.mods:nameIdentifier", "var": "$j")
      if any_equal("$j.type","orcid")
        paste("creator[].$last.id", "~https://orcid.org/", "$j.value", join_char:"")
      end
    end
  end
end

#  ------------contributor------------------------------------
set_array("contributor[]")
do list("path":"metadata.mods:mods.mods:name","var":"$i")
  if any_match("$i.type","corporate|personal")
    unless any_match("$i.mods:role.mods:roleTerm.*.value","aut|cre|his")
      copy_field("$i.mods:displayForm.value","contributor[].$append.name")
      if any_equal("$i.type","personal")
        add_field("contributor[].$last.type", "Person")
      end
      if any_equal("$i.type","corporate")
        add_field("contributor[].$last.type", "Organization")
      end
    end
  end
end

#  ------------sourceOrganization------------------------------------
set_array("sourceOrganization[]")
do list(path:"metadata.mods:mods.mods:name","var":"$i")
  if any_match("$i.mods:role.mods:roleTerm.*.value","his")
    unless exists("$i.mods:displayForm.value")
      lookup("$i.valueURI","data/maps/duepublico-corporate.tsv","sep_char":"\t", delete:"true") 
      copy_field("$i.valueURI","sourceOrganization[].$append.name")         
    end
    if exists("$i.mods:displayForm.value")
      copy_field("$i.mods:displayForm.value","sourceOrganization[].$append.name")
    end
  end
end

do list(path:"sourceOrganization[]")
  add_field("type", "Organization")
end

add_field("sourceOrganization[].$append.name", "Universit√§t Duisburg-Essen")
add_field("sourceOrganization[].$last.type", "Organization")

#  ------------description------------------------------------
copy_field("metadata.mods:mods.mods:abstract.*.value","description")

# -----------license----------
copy_field("metadata.mods:mods.mods:accessCondition.xlink:href","license.id")
lookup("license.id","data/maps/duepublico-licenses.tsv","sep_char":"\t", delete:"true") 

#  ------------dataCreated------------------------------------
copy_field("metadata.mods:mods.mods:originInfo.mods:dateIssued.value","dataCreated")

#  ------------inLanguage------------------------------------
set_array("inLanguage[]")
copy_field("metadata.mods:mods.mods:language.mods:languageTerm.value","inLanguage[].$append")

# ---------------type ----------------------------------
set_array("type[]", "LearningResource")

# ---------------learningResourceType ----------------------------------
set_array("learningResourceType[]")
set_array("@lRT")
do list(path:"metadata.mods:mods.mods:genre","var":"$i") 
  if any_match("$i.authorityURI","https://duepublico.uni-due.de/api/v1/classifications/mir_genres")
    lookup("$i.valueURI", "data/maps/duepublico-hcrt.tsv","sep_char":"\t", delete:"true")
    copy_field("$i.valueURI", "@lRT.$append")
  end
end
do list(path:"metadata.mods:mods.mods:typeOfResource","var":"$i")
  lookup("$i.value", "data/maps/duepublico_typeOfResource-hcrt.tsv","sep_char":"\t", delete:"true")
  copy_field("$i.value", "@lRT.$append")
end
uniq("@lRT")

do list(path: "@lRT", "var":"$i")
  copy_field("$i","learningResourceType[].$append.id")
  copy_field("$i","learningResourceType[].$last.prefLabel.de")
  copy_field("$i","learningResourceType[].$last.prefLabel.en")
end

lookup("learningResourceType[].*.prefLabel.de", "data/maps/hcrt-de-labels.tsv","sep_char":"\t", delete:"true")
lookup("learningResourceType[].*.prefLabel.en", "data/maps/hcrt-en-labels.tsv","sep_char":"\t", delete:"true")

# ---------------keywords----------------------------------
set_array("keywords[]")
# TODO: Keywords seem to be tokenized in the data mostly, but some are concatinated. Need to split and add to the array.
copy_field("metadata.mods:mods.mods:subject.mods:topic.*.value","keywords[].$append")

#  ------------about------------------------------------
set_array("about[]")
do list(path:"metadata.mods:mods.mods:classification","var":"$i")
  if any_equal("$i.authorityURI","https://duepublico.uni-due.de/api/v1/classifications/destatis")
    lookup("$i.valueURI", "data/maps/destatisPersonal2destatisStudierende.tsv","sep_char":"\t", delete:"true")
    copy_field("$i.valueURI","about[].$append.id")
    copy_field("$i.valueURI","about[].$last.prefLabel.de")
  end
end

lookup("about[].*.prefLabel.de", "data/maps/subject-labels.tsv","sep_char":"\t", delete:"true")


# -------isPartOf---------
set_array("isPartOf[]")
do list(path: "metadata.mods:mods.mods:relatedItem", "var": "$i")
  if any_equal("$i.type","host")
    copy_field("$i.xlink:href", "isPartOf[].$append.id")
    paste("isPartOf[].$last.name","$i.mods:titleInfo.mods:nonSort.value","$i.mods:titleInfo.mods:title.value")
  end
end

prepend("isPartOf[].*.id", "https://duepublico2.uni-due.de/receive/")

# ---------------conditionsOfAccess----------------------------------
add_field("conditionsOfAccess.id","http://w3id.org/kim/conditionsOfAccess/no_login") 


# ---------mainEntityOfPage----------------
set_array("mainEntityOfPage[]")
copy_field("id", "mainEntityOfPage[].$append.id")
add_field("mainEntityOfPage[].$last.provider.id","$[service_id]")
add_field("mainEntityOfPage[].$last.provider.type","Service")
add_field("mainEntityOfPage[].$last.provider.name","$[service_name]")

# ---------------@context ----------------------------------

set_array("@context[]", "https://w3id.org/kim/amb/draft/context.jsonld")
# resource is german therefore prefer de if multiple or no inLanguage are available
if any_equal("inLanguage[]","de")
  set_hash("@context[].$append", "@language": "de")
elsif any_equal("inLanguage[]","en")
  set_hash("@context[].$append", "@language": "en")
else
  copy_field("inLanguage[].1", "@context[].$append.@language")
end
# resources without inLanguage-Values set default to de
unless exists("inLanguage[].1")
  set_hash("@context[].$append", "@language": "de")
end

# ---------tidy up!----------------
include ("../../sharedFixes/cleanUp.fix")
vacuum()
