/* Set up the context */
do array("@context")
 add_field("","https://w3id.org/kim/lrmi-profile/draft/context.jsonld")
 do entity("")
  add_field("@language", "de")
 end
end

map("@id", "id")

map("name", "name")

map("image", "image")
map("description", "description")

do entity("license")
  do map("license", "id")
    regexp(match: '^http[s]?:\\/\\/creativecommons.org\\/(licenses|licences|publicdomain)\\/.*|^http[s]?:\\/\\/www.gnu.org\\/licenses\\/.*|^http[s]?:\\/\\/www.apache.org\\/licenses\\/.*|http[s]?://opensource.org/licenses/MIT|^http[s]?:\\/\\/www.opensource.org\\/licenses\\/BSD.*')
  end
end

/* The audicence is created differently now as object as well it is not provided often. */

do map("dateCreated", "dateCreated")
  replace_all(pattern: "T.*", with: "")
end

do map("dateModified", "dateModified")
  replace_all(pattern: "T.*", with: "")
end

do array("creator", flushWith: record)
 do entity("", flushWith: "author[].?|author[].??")
  map("author[].*.@type", "type")
  do map("author[].*.name", "name")
    replace(pattern: "Apl. |\\(apl.\\) |Dr. med |Dr. |Dr.'in |Dr |Dr.med.|Dres. |Dr.|Prof. |Prof.'in |Prof.|Prof |Jun.-|PD |PD. |med. |rer. |pol.|nat. |dent. |em. |Uni.-|Univ.-|Univ. |Dipl.|-Ing. |-Inform. |-Psych. |h.c. |mult. |, MME|; MME|, MScN|M. Sc. |, MSc| MSc.|, M.A.| M.A.|, M.D.|, B.A.|, MMZ|, Psychoanalytiker|, L.L.M.|, LL.M.| \\(M.A.\\)", with: "")
  end 
 end
end

do array("sourceOrganization", flushWith: record)
 do entity("")
  add_field("type","Organization")
  map("provider[].*.name", "name")
 end
end

/* Create array, split provided keyword string and trim blanks */
do array("keywords")
  do map("keywords")
    split(delimiter: ",")
    trim()
  end
end

do map("keywords", "@hochschulfaechersystematik")
  split(delimiter: ",")
  trim()
  lookup(in: "data/maps/hoou-subject-mapping.tsv")
  lookup(in: "data/maps/subject-url-mapping.tsv")
end

do array("about", flushWith: record)
 do entity("", flushWith: "@hochschulfaechersystematik")
  map("@hochschulfaechersystematik", "id")
  do map("@hochschulfaechersystematik", "prefLabel.de")
    lookup(in: "data/maps/subject-labels.tsv")
  end
 end
end

do array("type")
  add_field("","LearningResource")
end

/* HOOU provides at the moment only Course or CreativeWork as types. Also learningResourceType is set with Video oder Bilder but not very often. */
do map("@type|learningResourceType", "@hcrt")
  replace(pattern:"Bilder", with:"Image")
  lookup(in: "data/maps/en-labels-hcrt-uri.tsv")
  regexp(match: "https://w3id.org/kim/hcrt/.*")
end

do array("learningResourceType")
  do entity("")
    map("@hcrt", "id")
    do entity("prefLabel")
      do map("@hcrt", "de")
        lookup(in: "data/maps/hcrt-de-labels.tsv")
      end  
      do map("@hcrt", "en")
        lookup(in: "data/maps/hcrt-en-labels.tsv")
      end
    end
  end
end

do array("mainEntityOfPage")
  do entity("")
    map("@id", "id")
    /* Add provider/source information to each resource description */
    do entity("provider")
      add_field("id","$[service_id]")
      add_field("type","Service")
      add_field("name","$[service_name]")
    end
  end
end

/* Only little number of records have audience informations. */
do map("audience[].*.educationalRole", "@educationalAudienceRole")
  lookup("Lehrer*in": "teacher", "Sch√ºler*innen": "student", "Student*in": "student")
end

do array("audience" flushWith: record)
  do entity("", flushWith:"@educationalAudienceRole")
    do map("@educationalAudienceRole", "id")
      compose(prefix: "http://purl.org/dcx/lrmi-vocabs/educationalAudienceRole/")
      unique()
    end
  end
end