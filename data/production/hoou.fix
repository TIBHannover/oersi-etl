/* Set up the context */
do array("@context")
 add_field("","https://w3id.org/kim/lrmi-profile/draft/context.jsonld")
 do entity("")
  add_field("@language", "de")
 end
end

map("@id", "id")

map("name", "name")

map("image", "image")
map("description", "description")
map("license", "liscense")

/* The audicence is created differently now as object as well it is not provided often. */
/* map("audience", "audience") */

do map("dateCreated", "dateCreated")
  replace_all(pattern: "T.*", with: "")
end

do map("dateModified", "dateModified")
  replace_all(pattern: "T.*", with: "")
end

do array("creator", flushWith: record)
 do entity("", flushWith: "author[].?|author[].??")
  map("author[].*.@type", "type")
  map("author[].*.name", "name")
 end
end

do array("publisher", flushWith: record)
 do entity("")
  add_field("type","Organization")
  map("provider[].*.name", "name")
 end
end

/* Create array, split provided keyword string and trim blanks */
do array("keywords")
  do map("keywords")
    split(delimiter: ",")
    trim()
  end
end

do array("type")
  add_field("","LearningResource")
end

do array("mainEntityOfPage")
  do entity("")
    map("@id", "id")
    /* Add provider/source information to each resource description */
    do entity("provider")
      add_field("id","$[service_id]")
      add_field("type","Service")
      add_field("name","$[service_name]")
    end
  end
end