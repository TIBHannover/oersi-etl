# ---------------@context ----------------------------------

set_array("@context[]", "https://w3id.org/kim/lrmi-profile/draft/context.jsonld")
set_hash("@context[].$append", "@language": "de")

#  ------------id------------------------------------
move_field("attributes.url", "id")

#  ------------name------------------------------------
move_field("attributes.name", "name")

#  ------------dateCreated------------------------------------
move_field("attributes.startDate", "dateCreated")
replace_all("dateCreated", "T.*", "")

#  ------------image------------------------------------
move_field("attributes.moocProvider.logo", "image")

#  ------------creator------------------------------------
move_field("attributes.instructors[].*.name", "creator[].$append.name")
replace_all("creator[].$append.name", 'Apl\\. |\\(apl\\.\\) |Dr\\. med |Dr\\. |Dr\\.'in |Dr |Dr\\.med\\.|Dres\\. |Dr\\.|habil\\. |Prof\\. |Prof\\.'in |Prof\\.|Prof |Jun\\.-|PD |PD\\. |med\\. |rer\\. |pol\\.|nat\\. |dent\\. |em\\. |Ao\\.-|o\\. Univ\\.|Uni\\.-|Univ\\.-|Univ\\. |DI |RA |Priv\\.-Doz\\. |Dipl\\.|-Ing\\. |-Inform\\. |-Psych\\. |h\\.c\\. |mult\\. |Mag\\. |, MME|; MME|, MScN|M\\. Sc\\. |M\\.Sc\\. |Psych\\. |, MSc| MSc\\.|, M\\.A\\.| M\\.A\\.|, M\\.D\\.|, B\\.A\\.|, MMZ|, Psychoanalytiker|, L\\.L\\.M\\.|, LL\\.M\\.| \\(M\\.A\\.\\)|  \\(kommissarisch\\)', " ")
add_field("creator[].*.type", "Person")

#  ------------sourceOrganisation------------------------------------
split("attributes.university", ",")
move_field("attributes.university", "sourceOrganization[].$append.name")
do list(path:"sourceOrganization[]")
  add_field("type", "Organization")
end

add_field("sourceOrganization[].$append.name", "Virtuelle Hochschule Bayern")
add_field("sourceOrganization[].$last.type", "Organization")

#  ------------license------------------------------------
move_field("attributes.courseLicenses[].*.url", "license.id")

#  ------------inLanguage------------------------------------
move_field("attributes.languages","inLanguage[].$append")
do list(path:"inLanguage[]")
  if all_match(".", "|Mehrsprachig")
    remove_field(".")
  end
end

# ---------------type ----------------------------------
set_array("type[]", "LearningResource")

# ---------description---------
paste("description", "attributes.abstract", "attributes.targetgroup", "attributes.learningObjectives", join_char:”\n\n\n”)
replace_all("description", '\\w*[^>]*?\\w*=\\"(.*?)\\">', " ")
replace_all("description", '\\w*[^>]*?\\w*=\\"(.*?)\\" \\w*=\\"(.*?)\\>', " ")
replace_all("description", '<\\w*>|</\\w*>', " ")
replace_all("description", '<br />', " ")
replace_all("description", '  ', " ")
trim("description")

# ---------keywords---------
split_field('attributes.keywords', ",")
move_field('attributes.keywords', "keywords[]")

# ------------learningResourceType-----
add_field("learningResourceType[].$append.id","https://w3id.org/kim/hcrt/course")
add_field("learningResourceType[].$last.prefLabel.de", "Kurs")
add_field("learningResourceType[].$last.prefLabel.en","Course")

# ------about----
# sometimes the field is not an array. This needs to be settled:
lookup("subjectArea", "data/maps/destatisLabels-to-uri.tsv","sep_char":"\t")
do list(path: "fields[]", "var": f)
    copy_field("f", "about[].$append.id")
    lookup("f", "data/maps/subject-labels.tsv","sep_char":"\t")
    copy_field("f", "about[].$last.prefLabel.de")
end

/* mainEntityOfPage */
copy_field("id", "mainEntityOfPage[].$append.id")
add_field("mainEntityOfPage[].$last.provider.id","$[service_id]")
add_field("mainEntityOfPage[].$last.provider.type","Service")
add_field("mainEntityOfPage[].$last.provider.name","$[service_name]")

retain("id", "name", "dateCreated", "image", "creator[]", "sourceOrganization[]", "license", "inLanguage[]", "type[]", "learningResourceType[]", "about[]", "description", "keywords[]" "mainEntityOfPage[]")