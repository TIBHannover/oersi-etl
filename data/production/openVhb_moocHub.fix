/* Set up the context */
do array("@context")
 add_field("","https://w3id.org/kim/lrmi-profile/draft/context.jsonld")
 do entity("")
  add_field("@language", "de")
 end
end

/* id */
map("attributes.url", "id")

/* name */
map("attributes.name", "name")

/*image */
map("attributes.image.url", "image")

/* dateCreated */
do map("attributes.startDate", "dateCreated")
  replace(pattern: "T.*", with: "")
end


/* creator: source data is not consistent, can be a single author or multiple separated by comma or "&amp;". API estimates one object per instructor. Also there is no type even though there are persons and organisations.*/
do map("attributes.instructors[].*.name", "@creator")
  split(delimiter: ", | &amp; | & | /")
  replace(pattern: "Apl\\. |\\(apl\\.\\) |Dr\\. med |Dr\\. |Dr\\.'in |Dr |Dr\\.med\\.|Dres\\. |Dr\\.|habil\\. |Prof\\. |Prof\\.'in |Prof\\.|Prof |Jun\\.-|PD |PD\\. |med\\. |rer\\. |pol\\.|nat\\. |dent\\. |em\\. |Ao\\.-|o\\. Univ\\.|Uni\\.-|Univ\\.-|Univ\\. |DI |RA |Priv\\.-Doz\\. |Dipl\\.|-Ing\\. |-Inform\\. |-Psych\\. |h\\.c\\. |mult\\. |Mag\\. |, MME|; MME|, MScN|M\\. Sc\\. |M\\.Sc\\. |Psych\\. |, MSc| MSc\\.|, M\\.A\\.| M\\.A\\.|, M\\.D\\.|, B\\.A\\.|, MMZ|, Psychoanalytiker|, L\\.L\\.M\\.|, LL\\.M\\.| \\(M\\.A\\.\\)|  \\(kommissarisch\\)", with: "")
  not_equals(string: '')
end

do array("creator", flushWith: record)
 do entity("", flushWith: "@creator")
  add_field("type", "Person")
  map("@creator", "name")
 end
end



/* sourceOrganization */
do array("sourceOrganization", flushWith: record)
 do entity("")
  add_field("type", "Organization")
  add_field("name", "Virtuelle Hochschule Bayern")
 end
end

/* learningResourceType: Add Course always as lRT */
add_field("@hcrt","https://w3id.org/kim/hcrt/course")

do array('learningResourceType')
  do entity('')
    map('@hcrt', 'id')
    do entity('prefLabel')
      do map('@hcrt', 'de')
        lookup(in: 'data/maps/hcrt-de-labels.tsv')
      end  
      do map('@hcrt', 'en')
        lookup(in: 'data/maps/hcrt-en-labels.tsv')
      end
    end
  end
end

/* inLanguage: The value Mehrsprachig does not represent the estimated language labels. */
do array("inLanguage")
  do map("attributes.languages")
    not_equals(string: "")
    not_equals(string: "Mehrsprachig")
  end
end

/*license */
do entity("license")
  map("attributes.courseLicenses[].*.url","id")
end

/* type */
do array ("type")
  add_field("","LearningResource")
end

/* mainEntityofPage */
do array("mainEntityOfPage")
  do entity("")
    map("attributes.url", "id")
    /* Add provider/source information to each resource description */
    do entity("provider")
      add_field("id","$[service_id]")
      add_field("type","Service")
      add_field("name","$[service_name]")
    end
  end
end

/* keywords*/
do entity('keywords[]', flushWith: 'record')
  do map('attributes.keywords')
    split(delimiter: ",")
  end
end

/*description */
do concat(name: "@description", delimiter: "\n\n\n", flushWith: 'record')
  map("attributes.abstract")
  map("attributes.targetgroup")    
  map("attributes.learningObjectives")
end

do map("@description", "description")
  replace(pattern:'\\w*[^>]*?\\w*=\\"(.*?)\\">', with: " ")
  replace(pattern:'\\w*[^>]*?\\w*=\\"(.*?)\\">', with: " ")
  replace(pattern:'\\w*[^>]*?\\w*=\\"(.*?)\\" \\w*=\\"(.*?)\\>', with: " ")
  replace(pattern:"<\\w*>|</\\w*>|<!--(.*?)-->", with: " ")
  replace(pattern:"<br />", with: " ")
  replace(pattern:"  ", with: " ")  
  trim()
end

/*missing:*/
/*about*/
