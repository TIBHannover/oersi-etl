
# --------Delete all emtpy values-------
vacuum()

do once("macrosAndMaps")
  include ("../../sharedFixes/macros.fix")
  include ("../../sharedFixes/maps.fix")
  # workflow specific maps
  put_filemap("data/maps/orcaUni.tsv","orcaUni","sep_char":"\t")
  put_filemap("data/maps/orcaLabels-hcrt.tsv","orcaLabels-hcrt","sep_char":"\t") 
end


/* Map some of the data we have to the oersi model: */
# ---------------id --------
copy_field("URL_id", "id")

# ---------------name --------
copy_field("Titel_des_Materials", "name")


# ---------------description --------
copy_field("Kurzbeschreibung", "description")

# ---------------image --------
copy_field("Vorschaubild_URL","image")

# ---------------dateCreated --------
copy_field("PubDatum", "dateCreated")

# ---------------license --------
copy_field("Lizenz", "license.id")

# ------ creator --- 
set_array("creator[]")
copy_field("Urheber1_Name", "creator[].$append.name")
copy_field("Urheber1_Typ", "creator[].1.type")
copy_field("Urheber1_Hochschule-RoRID","creator[].1.@affiliation")
copy_field("Urheber1_ORCID","creator[].1.id")
copy_field("Urheber1_Grad", "creator[].1.honorificPrefix")

copy_field("Urheber2_Name", "creator[].$append.name")
copy_field("Urheber2_Typ", "creator[].2.type")
copy_field("Urheber2_Hochschule-RoRID","creator[].2.@affiliation")
copy_field("Urheber2_ORCID","creator[].2.id")
copy_field("Urheber2_Grad", "creator[].2.honorificPrefix")

set_array("@sourceOrga")
do list(path:"creator[]", "var":"$i")
  copy_field("$i.@affiliation", "$i.affiliation.id")
  copy_field("$i.@affiliation", "$i.affiliation.name")
  lookup("$i.affiliation.name", "orcaUni"delete:"true")
  add_field("$i.affiliation.type", "Organization")
  move_field("$i.@affiliation", "@sourceOrga.$append")
end

/* Contributor */
set_array("contributor[]")
copy_field("Beitragende1_Name", "contributor[].$append.name")
copy_field("Beitragende1_Typ", "contributor[].1.type")
copy_field("Beitragende1_Hochschule-RoRID","contributor[].1.@affiliation")
copy_field("Beitragende1_ORCID","contributor[].1.id")
copy_field("Beitragende1_Grad", "contributor[].1.honorificPrefix")

copy_field("Beitragende2_Name", "contributor[].$append.name")
copy_field("Beitragende2_Typ", "contributor[].2.type")
copy_field("Beitragende2_Hochschule-RoRID","contributor[].2.@affiliation")
copy_field("Beitragende2_ORCID","contributor[].2.id")
copy_field("Beitragende2_Grad", "contributor[].2.honorificPrefix")

do list(path:"contributor[]", "var":"$i")
  copy_field("$i.@affiliation", "$i.affiliation.id")
  copy_field("$i.@affiliation", "$i.affiliation.name")
  lookup("$i.affiliation.name", "orcaUni"delete:"true")
  add_field("$i.affiliation.type", "Organization")
  move_field("$i.@affiliation", "@sourceOrga.$append")
end


# ------ sourceOrganization ----
set_array("sourceOrganization[]")
uniq("@sourceOrga")

do list(path:"@sourceOrga", "var": "$i")
  copy_field("$i", "sourceOrganization[].$append.id")
  copy_field("$i", "sourceOrganization[].$last.name")
  add_field("sourceOrganization[].$last.type", "Organization")
end

lookup("sourceOrganization[].*.name", "orcaUni"delete:"true")

# ------ type --- 
set_array("type[]", "LearningResource")

# ------ learningResourceType */
set_array("learningResourceType[]")

copy_field("Medientyp", "@hcrt")
split_field("@hcrt", ";")
trim("@hcrt.*")
uniq("@hcrt")

do list(path:"@hcrt", "var":"$i")
  lookup("$i","orcaLabels-hcrt", delete:"true")
  copy_field("$i", "learningResourceType[].$append.id")
end

do list(path:"learningResourceType[]")
  copy_field("id", "prefLabel.de")
  lookup("prefLabel.de", "hcrt-deLabel2Uri", delete:"true")
  copy_field("id", "prefLabel.en")
  lookup("prefLabel.en", "hcrt-enLabel2Uri", delete:"true")
end

# ------ about ------

copy_field("Fachbereich", "@hochschulfaechersystematik")
split_field("@hochschulfaechersystematik", ";")
trim("@hochschulfaechersystematik.*")
uniq("@hochschulfaechersystematik")
lookup("@hochschulfaechersystematik.*","destatis-deLabel2Uri", delete:"true")


set_array("about[]")
copy_field("@hochschulfaechersystematik.*","about[].$append.id")

do list(path: "about[]")
  copy_field("id", "prefLabel.de")
  lookup("prefLabel.de","destatis-deLabel2Uri", delete:"true")
end

# ------ inLanguage------
set_array("inLanguage[]")
split_field("Sprache",";")
do list(path:"Sprache", "var":"$i")
  copy_field("$i", "inLanguage[]")
end
trim("inLanguage[].*")

# ------ keywords ------

copy_field("Tags_Schlagw√∂rter", "keywords[]")
split_field("keywords[]",";")
trim("keywords[].*")

# ---------------conditionsOfAccess----------------------------------
add_field("conditionsOfAccess.id","http://w3id.org/kim/conditionsOfAccess/no_login") 

# ---------mainEntityOfPage----------------
set_array("mainEntityOfPage[]")
copy_field("id", "mainEntityOfPage[].$append.id")
add_field("mainEntityOfPage[].$last.provider.id","$[service_id]")
add_field("mainEntityOfPage[].$last.provider.type","Service")
add_field("mainEntityOfPage[].$last.provider.name","$[service_name]")

# ---------------@context ----------------------------------

set_array("@context[]", "https://w3id.org/kim/amb/draft/context.jsonld")
# resource is german therefore prefer de if multiple or no inLanguage are available
if any_equal("inLanguage[]","de")
  set_hash("@context[].$append", "@language": "de")
elsif any_equal("inLanguage[]","en")
  set_hash("@context[].$append", "@language": "en")
else
  copy_field("inLanguage[].1", "@context[].$append.@language")
end
# resources without inLanguage-Values set default to de
unless exists("inLanguage[].1")
  set_hash("@context[].$append", "@language": "de")
end



# ------ tidy up --- 
include ("../../sharedFixes/cleanUp.fix")
