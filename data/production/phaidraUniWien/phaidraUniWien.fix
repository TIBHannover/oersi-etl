if exists ("metadata.error")
  reject()
end

if any_equal ("header.status", "deleted")
  reject()
end

# Delete all empty values
vacuum()

# ---------------id----------------------------------

copy_field("metadata.resource.datacite:identifier.value", "id")


# ---------------name----------------------------------
# The json-validator created an error due to duplicated element "name". Therefore the two conditionals should prevent a duplicate elemente.


do list(path:"metadata.resource.datacite:titles.datacite:title", "var": "$i")
  unless exists("name")
    unless exists("$i.titleType")
      copy_field("$i.value", "name")
    end
  end
end

# ---------------dateCreated----------------------------------

do list(path:"metadata.resource.datacite:dates.datacite:date","var":"$i")
  if any_equal("$i.dateType", "Issued")
    copy_field("$i.value","dateCreated")
  end
end
replace_all("dateCreated","^(\\d{4})$","$1-01-01")


# ---------------description----------------------------------

do list(path: "metadata.resource.dc:description", "var":"$i")
  unless exists("description")
    copy_field("$i.value","description")
  end
end

# ---------------creator----------------------------------
set_array("creator[]")
do list(path: "metadata.resource.datacite:creators.datacite:creator", "var": "$i")
  unless all_equal("$i.contributorType","Other")
    add_field("creator[].$append.test", "")
    copy_field("$i.datacite:creatorName.value", "creator[].$last.name")
    if any_equal("$i.datacite:creatorName.nameType","Organizational")
      add_field("creator[].$last.type", "Organization")
    else
      add_field("creator[].$last.type", "Person")
    end
    do list(path:"$i.datacite:nameIdentifier", "var":"$j")
      if any_equal("$j.nameIdentifierScheme","ORCID")
        copy_field("$j.value", "creator[].$last.id")
      end
    end
  end
end


replace_all("creator[].*.name","(.+), (.+)", "$2 $1")
prepend("creator[].*.id", "https://orcid.org/")
trim("creator[].*.id")



# ---------------contributors----------------------------------
set_array("contributor[]")
do list(path: "metadata.resource.datacite:contributors.datacite:contributor", "var": "$i")
  unless all_equal("$i.contributorType","Other")
    add_field("contributor[].$append.test", "")
    copy_field("$i.datacite:contributorName.value", "contributor[].$last.name")
    if any_equal("$i.datacite:contributorName.nameType","Organizational")
      add_field("contributor[].$last.type", "Organization")
    else
      add_field("contributor[].$last.type", "Person")
    end
    do list(path:"$i.datacite:nameIdentifier", "var":"$j")
      if any_equal("$j.nameIdentifierScheme","ORCID")
        copy_field("$j.value", "contributor[].$last.id")
      end
    end
  end
end


replace_all("contributor[].*.name","(.+), (.+)", "$2 $1")
prepend("contributor[].*.id", "https://orcid.org/")
trim("contributor[].*.id")

# ---------------license----------------------------------

unless any_equal("metadata.resource.licenseCondition.uri","http://rightsstatements.org/vocab/InC/1.0/")
  copy_field("metadata.resource.licenseCondition.uri","license.id")
end

# ------ type --- 

set_array("type[]", "LearningResource")

# ------------learningResourceType-----

set_array("learningResourceType[]")

do list(path:"metadata.resource.resourceType","var":"$i")
  copy_field("$i.uri", "learningResourceType[].$append.id")
end
do list(path:"learningResourceType[]","var":"$i")
  lookup("$i.id", "data/maps/coarResource2hcrt.tsv","sep_char":"\t", delete:"true")  
  copy_field("$i.id", "$i.prefLabel.de")
  lookup("$i.prefLabel.de", "data/maps/hcrt-de-labels.tsv","sep_char":"\t", delete:"true")
  copy_field("$i.id", "$i.prefLabel.en")
  lookup("$i.prefLabel.en", "data/maps/hcrt-en-labels.tsv","sep_char":"\t", delete:"true")
end

# ---------mainEntityOfPage----------------

set_array("mainEntityOfPage[]")
copy_field("id", "mainEntityOfPage[].$append.id")
add_field("mainEntityOfPage[].$last.provider.id","$[service_id]")
add_field("mainEntityOfPage[].$last.provider.type","Service")
add_field("mainEntityOfPage[].$last.provider.name","$[service_name]")

# ---------inLanguage----------------

set_array("inLanguage[]")
do list(path: "metadata.resource.dc:language", "var":"$i")
  copy_field("$i.value","inLanguage[].$append")
end  
lookup("inLanguage[].*",
  "fra": "fr",
  "deu":"de",
  "eng": "en"
  )

# ---------keywords----------------

set_array("keywords[]")
set_array("@about")
set_array("about[]")
do list(path:"metadata.resource.datacite:subjects.datacite:subject", "var":"$i")
  if any_contain("$i.value","ÖFOS")
    copy_field("$i.value", "@about.$append")
    split_field("@about.$last", " -- ")  
  else
    copy_field("$i.value", "keywords[].$append")
    if any_contain("keywords[].$last"," -- ")
      split_field("keywords[].$last", " -- ")
    else
      split_field("keywords[].$last", ",")
    end
  end
end


flatten("keywords[]")
flatten("@about")

do list(path: "@about", "var":"$i")
  unless any_equal("$i","ÖFOS 2012")
    copy_field("$i", "keywords[].$append")
    copy_field("$i", "about[].$append.id")
    replace_all("about[].$last.id",".*\\((\\d*)\\)","https://w3id.org/oerbase/vocabs/oefos2012/$1")
    lookup("about[].$last.id", "data/maps/oefos2destatis.tsv","sep_char":"\t")
  end
end

do list(path: "about[]", "var":"$i")
  copy_field("$i.id", "$i.prefLabel.de")
  lookup("$i.prefLabel.de", "data/maps/subject-labels.tsv","sep_char":"\t", delete:"true")
end

uniq("about[]")

# ------ encoding --- 

set_array("encoding[]")
if exists("metadata.resource.file")
  copy_field("metadata.resource.file.value","encoding[].$append.contentUrl")
  copy_field("metadata.resource.file.mimeType","encoding[].$last.mimeType")
  add_field("encoding[].$last.type", "MediaObject")
  if any_contain("id","o:")
    add_field("encoding[].$append.type", "MediaObject")
    copy_field("id", "encoding[].$last.embedUrl")
  end
end

replace_all("encoding[].*.embedUrl",".*(o:.*)","https://services.phaidra.univie.ac.at/api/object/$1/preview")

# -------isPartOf/hasPart---------
set_array("isPartOf[]")
set_array("hasPart[]")
do list(path: "metadata.resource.datacite:relatedIdentifiers.datacite:relatedIdentifier", "var": "$i")
  if any_equal("$i.relationType","IsPartOf")
    copy_field("$i.value", "isPartOf[].$append.id")
  elsif any_equal("$i.relationType","HasPart")
    copy_field("$i.value", "hasPart[].$append.id")
  end
end

# ---------------conditionsOfAccess----------------------------------
add_field("conditionsOfAccess.id","http://w3id.org/kim/conditionsOfAccess/no_login") 


# ---------------@context ----------------------------------

set_array("@context[]", "https://w3id.org/kim/amb/draft/context.jsonld")
# resource is german therefore prefer de if multiple or no inLanguage are available
if any_equal("inLanguage[]","de")
  set_hash("@context[].$append", "@language": "de")
elsif any_equal("inLanguage[]","en")
  set_hash("@context[].$append", "@language": "en")
else
  copy_field("inLanguage[].1", "@context[].$append.@language")
end
# resources without inLanguage-Values set default to de
unless exists("inLanguage[].1")
  set_hash("@context[].$append", "@language": "de")
end

# ------ tidy up --- 
include ("../../sharedFixes/retain.fix")
vacuum()
