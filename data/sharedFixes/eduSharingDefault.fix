#  ---------------name ----------------------------------

if exists("title")
  move_field("name", "@name")
  copy_field("title", "name")
end


#  ---------------image----------------------------------

copy_field("preview.url", "image")


#  ---------------id----------------------------------

copy_field("properties.ccm:wwwurl[].1", "@external_id")
trim("@external_id")
replace_all("@external_id", "\\u00a0", "") /* %A0 / &nbsp; */

copy_field("ref.id", "@internal_id")
prepend("@internal_id","https://$[service_domain]/edu-sharing/components/render/")

if exists("@external_id")
  if any_equal("properties.virtual:childobjectcount[]", "0")
    copy_field("@external_id", "id")
  elsif exists("properties.virtual:childobjectcount[]") # a series of objects. since the filters all "properties.virtual:childobjectcount[]" = "0", only the rest is kept in the second part of the conditional
    copy_field("@internal_id", "id")
  else
    copy_field("@external_id", "id")
  end
else
  copy_field("@internal_id", "id")
end


#  ---------mainEntityOfPage----------------

set_array("mainEntityOfPage[]")
copy_field("@internal_id", "mainEntityOfPage[].$append.id")
copy_field("modifiedAt", "mainEntityOfPage[].$last.dateModified")
copy_field("createdAt", "mainEntityOfPage[].$last.dateCreated")
add_field("mainEntityOfPage[].$last.provider.id","$[service_id]")
add_field("mainEntityOfPage[].$last.provider.type","Service")
add_field("mainEntityOfPage[].$last.provider.name","$[service_name]")

# ---------------description----------------------------------

copy_field("properties.cclom:general_description[].*", "description") # eventuell nur .1 nicht .*



#  ---------------about----------------------------------

set_array("@hochschulfaechersystematik[]")
copy_field("properties.ccm:taxonid[].*", "@hochschulfaechersystematik[].$append")
filter("@hochschulfaechersystematik[]", "^https://w3id.org/kim/hochschulfaechersystematik/.*")

set_array("about[]")
do list(path:"@hochschulfaechersystematik[]", "var":"$i")
  copy_field("$i", "about[].$append.id")
  copy_field("$i", "about[].$last.prefLabel.de")
end
lookup("about[].*.prefLabel.de", "destatis-deLabel2Uri", delete:"true")


#  ---------------creator----------------------------------

set_array("creator[]")
do list(path:"properties.ccm:lifecyclecontributer_author[]", "var": "$i")
  if any_match("$i", "^BEGIN:VCARD[\\s\\S]*FN:(.+?)\r?\n[\\s\\S]*")
    copy_field("$i", "creator[].$append.name")
    add_field("creator[].$last.type", "Person")
  elsif any_match("$i", "^BEGIN:VCARD\r?\nORG:(.+?)\r?\n[\\s\\S]*")
    copy_field("$i", "creator[].$append.name")
    add_field("creator[].$last.type", "Organization")
  end
end

do list(path:"creator[]")
  if any_match("name", "^BEGIN:VCARD[\\s\\S]*X-ORCID:.*(\\d{4}-\\d{4}-\\d{4}-\\d{4})\r?\n[\\s\\S]*")
    copy_field("name", "id")
    replace_all("id","^BEGIN:VCARD[\\s\\S]*X-ORCID:.*(\\d{4}-\\d{4}-\\d{4}-\\d{4})\r?\n[\\s\\S]*", "https://orcid.org/$1")
  end
  replace_all("name","^BEGIN:VCARD[\\s\\S]*FN:(.+?)\r?\n[\\s\\S]*", "$1")
  replace_all("name","^BEGIN:VCARD\r?\nORG:(.+?)\r?\n[\\s\\S]*", "$1")
end

#  ---------------contributor----------------------------------

set_array("contributor[]")
do list(path:"properties.ccm:lifecyclecontributer_*[]", "var": "$i")
  unless in("$i","properties.ccm:lifecyclecontributer_author[]")
    unless in("$i","properties.ccm:lifecyclecontributer_publisher[]")
      if any_match("$i", "^BEGIN:VCARD[\\s\\S]*FN:(.+?)\r?\n[\\s\\S]*")
        copy_field("$i", "contributor[].$append.name")
        add_field("contributor[].$last.type", "Person")
      elsif any_match("$i", "^BEGIN:VCARD\r?\nORG:(.+?)\r?\n[\\s\\S]*")
        copy_field("$i", "contributor[].$append.name")
        add_field("contributor[].$last.type", "Organization")
      end
    end
  end
end

do list(path:"contributor[]")
  if any_match("name", "^BEGIN:VCARD[\\s\\S]*X-ORCID:.*(\\d{4}-\\d{4}-\\d{4}-\\d{4})\r?\n[\\s\\S]*")
    copy_field("name", "id")
    replace_all("id","^BEGIN:VCARD[\\s\\S]*X-ORCID:.*(\\d{4}-\\d{4}-\\d{4}-\\d{4})\r?\n[\\s\\S]*", "https://orcid.org/$1")
  end
  replace_all("name","^BEGIN:VCARD[\\s\\S]*FN:(.+?)\r?\n[\\s\\S]*", "$1")
  replace_all("name","^BEGIN:VCARD\r?\nORG:(.+?)\r?\n[\\s\\S]*", "$1")
end


#  ---------------sourceOrganization----------------------------------  

set_array("sourceOrganization[]")
do list(path:"properties.ccm:university_DISPLAYNAME[]", "var": "$i")
  unless any_match("$i", "null null|- Alle -")
    copy_field("$i", "sourceOrganization[].$append.name")
    add_field("sourceOrganization[].$last.type", "Organization")
  end
end

do list(path:"properties.ccm:lifecyclecontributer_publisher[]", "var": "$i")
  if any_match("$i", "^BEGIN:VCARD\r?\nORG:(.+)\r?\n[\\s\\S]*\r?\nEND:VCARD$")
    copy_field("$i", "sourceOrganization[].$append.name")
    add_field("sourceOrganization[].$last.type", "Organization")
    replace_all("sourceOrganization[].$last.name","^BEGIN:VCARD\r?\nORG:(.+)\r?\n[\\s\\S]*\r?\nEND:VCARD$", "$1")
    replace_all("sourceOrganization[].$last.name","\\\\", "")
  end
end

do list(path:"properties.ccm:lifecyclecontributer_initiator[]", "var": "$i")
  if any_match("$i", "^BEGIN:VCARD\r?\nORG:(.+)\r?\n[\\s\\S]*\r?\nEND:VCARD$")
    copy_field("$i", "sourceOrganization[].$append.name")
    add_field("sourceOrganization[].$last.type", "Organization")
    replace_all("sourceOrganization[].$last.name","^BEGIN:VCARD\r?\nORG:(.+)\r?\n[\\s\\S]*\r?\nEND:VCARD$", "$1")
    replace_all("sourceOrganization[].$last.name","\\\\", "")
  end
end




#  ---------------license----------------------------------

if exists("license.url")
  move_field("license.url", "license.id")
else
  remove_field("license")
  copy_field("properties.virtual:licenseurl[].1", "license.id")
end

unless any_match("properties.ccm:commonlicense_key[]","^CC_.*")
  copy_field("properties.ccm:commonlicense_key[].1", "license.id")
  replace_all("license.id","MIT", "https://opensource.org/licenses/MIT")
  replace_all("license.id","GPL3", "https://www.gnu.org/licenses/gpl-3.0")
end
  
unless any_match("license.id","^http[s]?:\\/\\/creativecommons.org\\/(licenses|licences|publicdomain)\\/.*|^http[s]?:\\/\\/www.gnu.org\\/licenses\\/.*|^http[s]?:\\/\\/www.apache.org\\/licenses\\/.*|http[s]?://opensource.org/licenses/MIT|^http[s]?:\\/\\/www.opensource.org\\/licenses\\/BSD.*")
  remove_field("license")
end

# ---------------conditionsOfAccess----------------------------------

add_field("conditionsOfAccess.id","http://w3id.org/kim/conditionsOfAccess/no_login")


# ---------------inLanguage----------------------------------

set_array("inLanguage[]")
copy_field("properties.cclom:general_language[].*", "inLanguage[].$append")

replace_all("inLanguage[].*","_..$", "") /* remove country suffixes eg. _DE */
replace_all("inLanguage[].*","^$", "de") /* empty strings default to 'de' */
replace_all("inLanguage[].*","unknown", "de")

# ---------------learningResourceType----------------------------------

set_array("learningResourceType[]")
do list(path:"properties.ccm:educationallearningresourcetype[]", "var": "$i")
  unless any_equal("$i", "null null")
    copy_field("$i", "learningResourceType[].$append.id")
    copy_field("$i", "learningResourceType[].$last.prefLabel.de")
    copy_field("$i", "learningResourceType[].$last.prefLabel.en")
  end
end

lookup("learningResourceType[].*.prefLabel.de", "hcrt-deLabel2Uri", delete:"true")
lookup("learningResourceType[].*.prefLabel.en", "hcrt-enLabel2Uri", delete:"true")


#  ---------------keywords----------------------------------
set_array("keywords[]")
do list(path: "properties.cclom:general_keyword[]","var":"$i")
  copy_field("$i", "keywords[].$append")
end

# Some eduSharing provider use GND classifications (classification_keyword).
# For those we only should use the lassification_keyword_display since classification_keyword entails the id.

if exists("properties.ccm:classification_keyword_display[]")
  do list(path: "properties.ccm:classification_keyword_display[]","var":"$i")
    copy_field("$i", "keywords[].$append")
  end
else
  do list(path: "properties.ccm:classification_keyword[]","var":"$i")
    copy_field("$i", "keywords[].$append")
  end
end

# ---------------encoding----------------------------------

unless exists("properties.ccm:wwwurl[].1")
  if exists("downloadUrl")
    set_array("encoding[]")
    add_field("encoding[].$append.type", "MediaObject")
    copy_field("downloadUrl", "encoding[].$last.contentUrl")
    copy_field("mimetype", "encoding[].$last.encodingFormat")
    copy_field("size", "encoding[].$last.contentSize")
  end
end


call_macro("type")
call_macro("contextRessourceInGerman")