#  ---------------id----------------------------------
remove_field("id")
copy_field("attributes.url", "id")

#  ---------------name----------------------------------
copy_field("attributes.name", "name")

#  ---------------image----------------------------------
copy_field("attributes.image.url", "image")
copy_field("attributes.image.contentUrl", "image")

#  ---------------dateCreated----------------------------------
copy_field("attributes.startDate", "dateCreated")
replace_all("dateCreated", "T.*", "")

#  ---------------dateCreated----------------------------------
copy_field("attributes.duration", "duration")


# ------ learningResourceType --- 
add_field("@hcrt","https://w3id.org/kim/hcrt/course")
# only course

add_array("learningResourceType[]")
add_field("learningResourceType[].$append.id","https://w3id.org/kim/hcrt/course")
add_field("learningResourceType[].$last.prefLabel.de","Kurs")
add_field("learningResourceType[].$last.prefLabel.en","Course")

# ------ inLanguage --- 
/* inLanguage: The value Mehrsprachig does not represent the estimated language labels. */
add_array("inLanguage[]")
copy_field("attributes.inLanguage[].*", "inLanguage[].$append")
copy_field("languages[].*", "inLanguage[].$append")

do list(path:"inLanguage[]","var":"$i")
  if any_equal("$i", "Mehrsprachig")
    remove_field("$i")
  end
end

# ------ keywords ---
copy_field("attributes.keywords[].*", "keywords[].$append")

# ------ license ---
copy_field("attributes.courseLicenses[].*.url","license.id")
copy_field("attributes.license[].*.id","license.id")

# ------ type --- 

add_array("type[]", "LearningResource")


# ------ trailer --- 
copy_field("attributes.video.url","trailer.embedUrl")
copy_field("attributes.trailer.contentUrl","trailer.embedUrl")
if exists("trailer.embedUrl")
	add_field("trailer.type","VideoObject")
end

replace_all("trailer.embedUrl","https://youtu.be/(.*)","https://www.youtube-nocookie.com/embed/$1")
replace_all("trailer.embedUrl","https://www.youtube.com/watch?v=(.*)","https://www.youtube-nocookie.com/embed/$1")

# ---------mainEntityOfPage----------------

add_array("mainEntityOfPage[]")
copy_field("id", "mainEntityOfPage[].$append.id")
add_field("mainEntityOfPage[].$last.provider.id","$[service_id]")
add_field("mainEntityOfPage[].$last.provider.type","Service")
add_field("mainEntityOfPage[].$last.provider.name","$[service_name]")

# ---------------@context ----------------------------------

add_array("@context[]", "https://w3id.org/kim/amb/context.jsonld")
# resource is german therefore prefer de if multiple or no inLanguage are available
if any_equal("inLanguage[]","de")
  add_hash("@context[].$append", "@language": "de")
elsif any_equal("inLanguage[]","en")
  add_hash("@context[].$append", "@language": "en")
else
  copy_field("inLanguage[].1", "@context[].$append.@language")
end
# resources without inLanguage-Values set default to de
unless exists("inLanguage[].1")
  add_hash("@context[].$append", "@language": "de")
end